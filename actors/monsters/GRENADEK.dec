Actor GrenadeKnight : Enemy 13448 {
	//$Category Federation
	Health 350
	GibHealth 60
	Radius 16
	Height 40
	Mass 100
	Speed 15
	
	+MISSILEMORE
	+MISSILEEVENMORE
	+FASTER
	+NORADIUSDMG

	DropItem "Clip" 128
	DropItem "Clip" 64
	DropItem "RocketAmmo" 128
	DropItem "RocketAmmo" 64
	Dropitem "RocketBox" 16

	Dropitem "Stimpack" 32
	Dropitem "GrenadierArmor" 4
	DropItem "ArmorShardSpawner" 64

	DropItem "Chaingun" 4

	//Dropitem "PDA" 4

	Dropitem "ABeacon" 2
	Dropitem "6Beacon" 2

	Translation "112:127=5:8"

	BloodType "AeniBlood"
	Decal BulletChip
	
	PainChance 50
	PainChance "Shock", 255
	PainChance "ShockSlug", 255

	SeeSound "Knight/KnightSight"
	AttackSound "MP7/FIRE"
	PainSound "Knight/KnightPain"
	DeathSound "monster/inidth"
	ActiveSound "monster/iniact"

	Obituary "%o was picked off by a Grenade Knight." 

	States {
  
/******************************************************************************\
==================================MAIN STATES===================================
\******************************************************************************/

	Spawn:
		HEVY A 0
		TNT1 A 0 Thing_SetSpecial(0,226,642,0,random(30,40))
	SpawnLoop:
		TNT1 A 0 A_SpawnItem("HeadshotTarget10",15,40,0)
		GO99 A 10 A_LookEx(0,0,random(512,2048),random(1024,4096),random(90,120),"See")
		Loop
	See:
		GO99 A 1 // Infinite loop crash prevention?
		Goto IfLowHealth
	IfLowHealth:
		TNT1 A 0 A_JumpIfHealthLower(50,"LowHealth")
		TNT1 A 0 A_TakeInventory("DealWithIt",1)
		Goto HealthHigh
	HealthHigh:
		TNT1 A 0 A_ChangeFlag("Frightened",0)
		Goto Chase
	LowHealth:
		TNT1 A 0 A_Jump(32,"HealUp")
		TNT1 A 0 A_TakeInventory("DealWithIt",1)
		Goto Retreat
	Retreat:
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_ChangeFlag("Frightened",1)
		Goto HealUp
	HealUp:
		TNT1 A 0 HealThing(1)
		Goto Chase
	Chase:
		TNT1 A 0 A_SpawnItem("HeadshotTarget3",15,40,0)
		GO99 AA 3 A_Chase
		TNT1 A 0 A_SpawnItem("HeadshotTarget3",15,40,0)
		GO99 BB 3 A_Chase
		TNT1 A 0 A_SpawnItem("HeadshotTarget3",15,40,0)
		GO99 CC 3 A_Chase
		TNT1 A 0 A_SpawnItem("HeadshotTarget3",15,40,0)
		GO99 DD 3 A_Chase
		Goto See

/******************************************************************************\
=================================ATTACK STATES==================================
\******************************************************************************/

	Missile:
		TNT1 A 0 A_SpawnItem("HeadshotTarget10",0,40,0)
		GO99 E 10 A_FaceTarget
		Goto DistanceCheck
	DistanceCheck:
		TNT1 A 0 A_SpawnItem("HeadshotTarget4",0,40,0)
		GO99 E 0 A_JumpIfCloser(192,"KnifeLunge")
		GO99 E 0 A_JumpIfCloser(1384,"LaunchHEGrenadeShort")
		GO99 E 0 A_JumpIfCloser(1704,"LaunchHEGrenade")
		GO99 E 0 A_JumpIfCloser(2024,"LaunchHEGrenadeFar")
		TNT1 A 0 A_TakeInventory("DealWithIt",1)
		Goto LaunchHEGrenadeDistant
	KnifeLunge:
		TNT1 A 0 ThrustThing(angle*256/360,25,0,0)
		GO99 E 4 A_FaceTarget
		TNT1 A 0 A_PlaySound("knife/swing")
		TNT1 A 0 A_SpawnItem("HeadshotTarget2",0,40,0)
		GO99 E 2 A_CustomMeleeAttack(34,"knife/swing","knife/swing","Knife",1)
		Goto CloseRangeFinish
	CloseRangeFinish:
		GO99 EE 3 A_SpawnItem("HeadshotTarget3",0,40,0)
		Goto See
	LaunchHEGrenadeShort:
		GO99 F 4 BRIGHT A_CustomMissile("KnightGrenadeShell",28,5,random(-1,1),2,random(0,10))
		Goto LaunchGrenadeEnd
	LaunchHEGrenade:
		GO99 F 4 BRIGHT A_CustomMissile("KnightGrenadeShell",28,5,random(-5,5),2,random(0,10))
		Goto LaunchGrenadeEnd
	LaunchHEGrenadeFar:
		GO99 F 4 BRIGHT A_CustomMissile("KnightGrenadeShell",28,5,random(-10,10),2,random(0,10))
		Goto LaunchGrenadeEnd
	LaunchHEGrenadeDistant:
		GO99 F 4 BRIGHT A_CustomMissile("KnightGrenadeShell",28,5,random(-15,15),2,random(0,10))
		Goto LaunchGrenadeEnd
	LaunchGrenadeEnd:
		GO99 EE 3 A_SpawnItem("HeadshotTarget3",0,40,0)
		Goto GrenadeReload
	GrenadeReload:
		TNT1 A 0 A_ChangeFlag(NOPAIN,1)
		TNT1 A 0 A_PlaySoundEx("M4_CLIPO","SoundSlot5")
		GO99 E 2 A_SpawnItem("HeadshotTarget2",0,40,0)
		GO99 E 10 A_SpawnItem("HeadshotTarget10",0,40,0)
		TNT1 A 0 A_PlaySoundEx("M4_CLIPI","SoundSlot5")
		GO99 E 2 A_SpawnItem("HeadshotTarget2",0,40,0)
		GO99 EEEEEE 10 A_SpawnItem("HeadshotTarget10",0,40,0)
		TNT1 A 0 A_ChangeFlag(NOPAIN,0)
		Goto See
	
/******************************************************************************\
==================================PAIN STATES===================================
\******************************************************************************/

	Pain:
		GO99 G 3 A_SpawnItem("HeadshotTarget4",0,40,0)
		TNT1 A 0 A_SpawnItem("HeadshotTarget4",0,40,0)
		GO99 G 3 A_Pain
		Goto Missile
	Pain.Shock:
	Pain.ShockSlug:
		TNT1 A 0 A_ChangeFlag(NOPAIN,1)
		TNT1 A 0 A_Pain
		GO99 GGGGGGGGGGGGGGGGGGGGGGGGGGGGGG 10 A_SpawnItem("HeadshotTarget10",0,40,0)
		TNT1 A 0 A_ChangeFlag(NOPAIN,0)
		Goto See
	Pain.KillMe:
		Goto See
		
/******************************************************************************\
==================================DEATH STATES==================================
\******************************************************************************/

	Death: 
		TNT1 A 0 A_JumpIfHealthLower(-60,"Splode")
		GO99 H 5 A_AlertMonsters
		GO99 I 5 A_Scream
		Goto DeathFall
	Death.KnifeSlash:
	Death.KnifeStab:
	Death.KnifeSlashPower:
	Death.KnifeStabPower:
	Death.WakizashiSlash:
	Death.WakizashiStab:
	Death.WakizashiSlashPower:
	Death.WakizashiStabPower:
		GO99 H 5
		GO99 I 5 A_PlaySound("KNI_HIT")
	DeathFall:
		GO99 J 5 A_Fall
		GO99 KLM 5
		TNT1 A 0 A_Jump(5,"Regenerating")
		GO99 N -1
		Stop
	Regenerating:
		GO99 NNNNNNNNNNNNNNN 70
		GO99 N -1 Thing_Raise(0)
		Wait
	Splode:
		GO99 I 1 A_SpawnDebris("NashGore_Gib9",0)
		TNT1 A 0 A_SpawnDebris("NashGore_Gib10",0)
		TNT1 A 0 A_SpawnDebris("NashGore_Gib11",0)
		TNT1 A 0 A_SpawnDebris("NashGore_Gib11",0)
		TNT1 A 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,0)
		TNT1 A 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,0)
		TNT1 A 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,0)
		TNT1 A 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,0)
		TNT1 A 0 A_XScream
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_SpawnItemEx("DEADInitiateHead",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
		Stop
	Death.Fire2:
		TNT1 A 1 A_SpawnItem("BurnDeath1",2,1)
		TNT1 A 0 A_AlertMonsters
		Stop
	Raise: 
		GO99 KJI 5
		GO99 H 5 Thing_SetSpecial(0,226,642,0,random(30,40))
		Goto See
	Death.head:
		GO99 O 9 A_PlaySound("headshot/splat")
		TNT1 A 0 A_SpawnItem("AeniBlood",0,40,0)
		TNT1 A 0 A_SpawnItem("AeniBlood",0,40,0)
		GO99 P 9 A_XScream
		GO99 Q 9 A_Fall
		GO99 R 5
		GO99 S 5 A_SpawnItemEx("DEADInitiate",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
		stop
	}
}

Actor GrenadeKnightGrenade1
{
  obituary "%o was fragged by a Grenade Knight."
  radius 6
  height 8
  speed 20
  damage (10)
  translation "16:47=160:167", "168:191=160:167", "1:1=0:0", "64:79=5:8", "128:151=5:8", "236:239=5:8"
  seesound "ZSec/bounce"
  PROJECTILE
  +DOOMBOUNCE
  -NOGRAVITY
  +GRENADETRAIL
  +DONTFALL
  bouncefactor 0.5
  bouncecount 10
  states
  {
  Spawn:
    GRND AAA 35 bright
  Death:
    TNT1 A 0 A_PlaySound("weapons/rocklx")
    TNT1 A 0 A_ChangeFlag(NoGravity, 1)
    TNT1 A 0 A_SpawnItemEx("DustCloudClusterLarge",0,0,0,0,0,0,0,0,0)
    MISL C 5 bright A_Explode(10,128,0)
    MISL D 4 bright
    MISL E 3 bright
    stop
  }
}

Actor GrenadeKnightGrenade2
{
  obituary "%o was fragged by a Grenade Knight."
  radius 6
  height 8
  speed 35
  damage (10)
  translation "16:47=160:167", "168:191=160:167", "1:1=0:0", "64:79=5:8", "128:151=5:8", "236:239=5:8"
  seesound "ZSec/bounce"
  PROJECTILE
  +DOOMBOUNCE
  -NOGRAVITY
  +GRENADETRAIL
  +DONTFALL
  bouncefactor 0.5
  bouncecount 10
  states
  {
  Spawn:
    GRND AAA 35 bright
    GRND AAA 35 bright
  Death:
    TNT1 A 0 A_PlaySound("weapons/rocklx")
    TNT1 A 0 A_ChangeFlag(NoGravity, 1)
    TNT1 A 0 A_SpawnItemEx("DustCloudClusterLarge",0,0,0,0,0,0,0,0,0)
    MISL C 5 bright A_Explode(10,128,0)
    MISL D 4 bright
    MISL E 3 bright
    stop
  }
}

Actor GrenadeKnightGrenade3
{
  obituary "%o was fragged by a Grenade Knight."
  radius 6
  height 8
  speed 50
  damage (10)
  translation "16:47=160:167", "168:191=160:167", "1:1=0:0", "64:79=5:8", "128:151=5:8", "236:239=5:8"
  seesound "ZSec/bounce"
  PROJECTILE
  +DOOMBOUNCE
  -NOGRAVITY
  +GRENADETRAIL
  +DONTFALL
  bouncefactor 0.5
  bouncecount 10
  states
  {
  Spawn:
    GRND AAA 35 bright
    GRND AAA 35 bright
    GRND AAA 35 bright
  Death:
    TNT1 A 0 A_PlaySound("weapons/rocklx")
    TNT1 A 0 A_ChangeFlag(NoGravity, 1)
    TNT1 A 0 A_SpawnItemEx("DustCloudClusterLarge",0,0,0,0,0,0,0,0,0)
    MISL C 5 bright A_Explode(10,128,0)
    MISL D 4 bright
    MISL E 3 bright
    stop
  }
}
