ACTOR RookKnight : Enemy 14942 {
	//$Category Federation
	
	Health 470
	GibHealth 60
	Radius 16
	Height 40
	Mass 500
	Speed 8
	
	+MISSILEMORE
	+MISSILEEVENMORE
	+FASTER
	+NORADIUSDMG

	DropItem "Clip" 128
	DropItem "RocketAmmo" 128
	DropItem "RocketAmmo" 64
	DropItem "RocketBox" 64

	Dropitem "Stimpack" 32
	Dropitem "RookArmor" 4
	DropItem "ArmorShardSpawner" 64

	DropItem "Chaingun" 4
	//DropItem "GrenadeLauncher" 1

	DropItem "StimPack" 4
	//Dropitem "PDA" 4

	DropItem "8Beacon" 8
	DropItem "ShieldGenerator" 16

	Translation "112:127=192:207"
	
	BloodType "AeniBlood"
	Decal "BulletChip"
	
	PainChance 50
	PainChance "Shock", 255
	PainChance "ShockSlug", 255
	
	SeeSound "Knight/KnightSight"
	AttackSound "MP7/FIRE"
	PainSound "Knight/KnightPain"
	DeathSound "monster/inidth"
	ActiveSound "monster/iniact"
	
	Obituary "%o was bombarded by a Rook Knight!" 
	
	States {

/******************************************************************************\
==================================MAIN STATES===================================
\******************************************************************************/

	Spawn:
		GO99 A 0
		TNT1 A 0 Thing_SetSpecial(0,226,642,0,random(60,80))
	SpawnLoop:
		TNT1 A 0 A_SpawnItem ("HeadshotTarget10",15,40,0)
		GO99 E 1 A_LookEx(0,0,random(1024,4096),random(1024,4096),random(90,120),"See")
		Loop
	See:
		TNT1 A 0 A_JumpIfHealthLower(50,"LowHealth")
		TNT1 A 0 A_TakeInventory("DealWithIt",1)
		Goto HealthHigh
	HealthHigh:
		TNT1 A 0 A_ChangeFlag("Frightened",0)
		Goto Chase
	LowHealth:
		TNT1 A 0 A_Jump(32,"HealUp")
		TNT1 A 0 A_TakeInventory("DealWithIt",1)
		Goto Retreat
	Retreat:
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_ChangeFlag("Frightened",1)
		Goto HealUp
	HealUp:
		TNT1 A 0 HealThing(1)
		Goto Chase
	Chase:
		TNT1 A 0 A_SpawnItem("HeadshotTarget3",15,40,0)
		GO99 AA 3 A_Chase
		TNT1 A 0 A_SpawnItem("HeadshotTarget3",15,40,0)
		GO99 BB 3 A_Chase
		TNT1 A 0 A_SpawnItem("HeadshotTarget3",15,40,0)
		GO99 CC 3 A_Chase
		TNT1 A 0 A_SpawnItem("HeadshotTarget3",15,40,0)
		GO99 DD 3 A_Chase
		TNT1 A 0 HealThing(1)
		Loop

/******************************************************************************\
=================================ATTACK STATES==================================
\******************************************************************************/

	Missile:
		TNT1 A 0 A_SpawnItem("HeadshotTarget10",0,40,0)
		GO99 E 10 A_FaceTarget
		GO99 E 0 A_Jump(64,"DistanceCheck")
		Goto BulletAttack
	BulletAttack:
		TNT1 A 0 A_JumpIfInventory("KnightAmmo",30,"Reload")
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySoundEx("MP7/FIRE","SoundSlot6")
		TNT1 A 0 A_GiveInventory("KnightAmmo",1)
		TNT1 A 0 A_SpawnItem("HeadshotTarget1",0,40,0)
		GO99 E 1 BRIGHT A_CustomMissile("RookTracer",38,0,random(-5,5),0,random(-5,5))
		GO99 E 2 A_SpawnItem("HeadshotTarget2",0,40,0)
		GO99 E 0 A_JumpIfInventory("KnightAmmo",30,"Reload")
		GO99 E 0 A_MonsterRefire(224,"See")
		Goto Loop
	Reload:
		TNT1 A 0 A_ChangeFlag(NOPAIN,1)
		TNT1 A 0 A_TakeInventory("KnightAmmo",30)
		GO99 EE 3 A_SpawnItem("HeadshotTarget3",0,40,0)
		TNT1 A 0 A_PlaySound("FNS_R2")
		GO99 E 2 A_SpawnItem("HeadshotTarget2",0,40,0)
		GO99 EEEEEEEEEEEEEEEEEEEEEEE 4 A_SpawnItem("HeadshotTarget4",0,40,0)
		TNT1 A 0 A_ChangeFlag(NOPAIN,0)
		goto See
	DistanceCheck:
		TNT1 A 0 A_JumpIfCloser(768,"ShortRangeBombard")
		TNT1 A 0 A_TakeInventory("DealWithIt",1)
		Goto LongRangeBombard
	LongRangeBombard:
		TNT1 A 0 A_ChangeFlag(NOPAIN,1)
		GO99 E 8 A_FaceTarget
		TNT1 A 0 A_SpawnItemEx("ShieldSpawner",0,96,0,0,0,0,0,32)
		TNT1 A 0 A_SpawnItemEx("ShieldSpawner",0,96,0,0,0,0,90,32)
		TNT1 A 0 A_SpawnItemEx("ShieldSpawner",0,96,0,0,0,0,180,32)
		TNT1 A 0 A_SpawnItemEx("ShieldSpawner",0,96,0,0,0,0,-90,32)
		TNT1 A 0 A_PlaySound("monster/Spreadsh")
		TNT1 A 0 A_SpawnItem("HeadshotTarget4",0,40,0)
		GO99 F 4 BRIGHT A_CustomMissile("RookKnightArtillery1",48,10,0,2,10)
		TNT1 A 0 A_PlaySound("monster/Spreadsh")
		TNT1 A 0 A_SpawnItem("HeadshotTarget4",0,40,0)
		GO99 F 4 BRIGHT A_CustomMissile("RookKnightArtillery2",48,-10,0,2,11)
		TNT1 A 0 A_PlaySound("monster/Spreadsh")
		TNT1 A 0 A_SpawnItem("HeadshotTarget4",0,40,0)
		GO99 F 4 BRIGHT A_CustomMissile("RookKnightArtillery3",48,10,0,2,12)
		TNT1 A 0 A_PlaySound("monster/Spreadsh")
		TNT1 A 0 A_SpawnItem("HeadshotTarget4",0,40,0)
		GO99 F 4 BRIGHT A_CustomMissile("RookKnightArtillery4",48,-10,0,2,13)
		TNT1 A 0 A_PlaySound("monster/Spreadsh")
		TNT1 A 0 A_SpawnItem("HeadshotTarget4",0,40,0)
		GO99 F 4 BRIGHT A_CustomMissile("RookKnightArtillery5",48,10,0,2,14)
		TNT1 A 0 A_PlaySound("monster/Spreadsh")
		TNT1 A 0 A_SpawnItem("HeadshotTarget4",0,40,0)
		GO99 F 4 BRIGHT A_CustomMissile("RookKnightArtillery6",48,-10,0,2,15)
		TNT1 A 0 A_PlaySound("monster/Spreadsh")
		TNT1 A 0 A_SpawnItem("HeadshotTarget4",0,40,0)
		GO99 F 4 BRIGHT A_CustomMissile("RookKnightArtillery7",48,10,0,2,16)
		TNT1 A 0 A_PlaySound("monster/Spreadsh")
		TNT1 A 0 A_SpawnItem("HeadshotTarget4",0,40,0)
		GO99 F 4 BRIGHT A_CustomMissile("RookKnightArtillery8",48,-10,0,2,17)
		TNT1 A 0 A_SpawnItem("HeadshotTarget4",0,40,0)
		GO99 E 4 A_FaceTarget
		TNT1 A 0 A_ChangeFlag(NOPAIN,0)
		Goto See
	ShortRangeBombard:
		TNT1 A 0 A_CustomMissile("MiniBomb",48,10,Random(-45,45),2,Random(0,45))
		TNT1 A 0 A_CustomMissile("MiniBomb",48,-10,Random(-45,45),2,Random(0,45))
		TNT1 A 0 A_CustomMissile("MiniBomb",48,10,Random(-45,45),2,Random(0,45))
		TNT1 A 0 A_CustomMissile("MiniBomb",48,-10,Random(-45,45),2,Random(0,45))
		TNT1 A 0 A_CustomMissile("MiniBomb",48,10,Random(-45,45),2,Random(0,45))
		TNT1 A 0 A_CustomMissile("MiniBomb",48,-10,Random(-45,45),2,Random(0,45))
		TNT1 A 0 A_CustomMissile("MiniBomb",48,10,Random(-45,45),2,Random(0,45))
		GO99 E 5 BRIGHT A_CustomMissile("MiniBomb",48,-10,Random(-45,45),2,Random(0,45))
		Goto See
		
/******************************************************************************\
==================================PAIN STATES===================================
\******************************************************************************/
		
	Pain:
		GO99 G 3 A_SpawnItem("HeadshotTarget4",0,40,0)
		TNT1 A 0 A_SpawnItem("HeadshotTarget4",0,40,0)
		GO99 G 3 A_Pain
		goto See
	Pain.Shock:
	Pain.ShockSlug:
		GO99 G 2 A_ChangeFlag(NOPAIN,1)
		TNT1 A 0 A_Pain
		GO99 GGGGGGGGGGGGGGGGGGGGGGGGGGGGGG 10 A_SpawnItem("HeadshotTarget10",0,40,0)
		TNT1 A 0 A_ChangeFlag(NOPAIN,0)
		Goto See
	Pain.KillMe:
		Goto See
		
/******************************************************************************\
==================================DEATH STATES==================================
\******************************************************************************/
		
	Death: 
		TNT1 A 0 A_JumpIfHealthLower(-60,"Splode")
		GO99 H 5 A_AlertMonsters
		GO99 I 5 A_Scream
		Goto DeathFall
	Death.KnifeSlash:
	Death.KnifeStab:
	Death.KnifeSlashPower:
	Death.KnifeStabPower:
	Death.WakizashiSlash:
	Death.WakizashiStab:
	Death.WakizashiSlashPower:
	Death.WakizashiStabPower:
		GO99 H 5
		GO99 I 5 A_PlaySound("KNI_HIT")
	DeathFall:
		GO99 J 5 A_Fall
		GO99 KLM 5
		GO99 N 0 A_Jump(5,"Regenerating")
		GO99 N -1
		stop
	Regenerating:
		GO99 NNNNNNNNNNNNNNN 70
		GO99 N -1 Thing_Raise(0)
		Wait
	Splode:
		TNT1 A 0 A_XScream
		TNT1 A 0 A_NoBlocking
		GO99 O 5 A_SpawnItem("AeniBlood",0,32,0)
		TNT1 A 0 A_SpawnItemEx("AeniBlood",0,34,0,random(-75,75)*0.01,random(-75,75)*0.01,random(-65,65)*0.01,0,SXF_NOCHECKPOSITION|SXF_CLIENTSIDE|SXF_TRANSFERTRANSLATION,0)	
		GO99 P 5 A_SpawnItem("AeniBlood",0,34,0)
		GO99 Q 5 A_SpawnItem("AeniBlood",0,26,0)
		GO99 R 5 A_SpawnItem("AeniBlood",0,20,0)
		GO99 S 5 A_SpawnItem("AeniBlood",0,14,0)
		GO99 T 5 A_SpawnItem("AeniBlood",0,6,0)
		GO99 U 5 A_SpawnItem("AeniBlood",0,0,0)
		GO99 V 5 A_SpawnItem("AeniBlood",0,0,0)
		GO99 W 5 // No extra blood needed here. He's dead, Jim.
		TNT1 A 0 A_SpawnItemEx("DEADStrikeKnightGib",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
		stop
	Death.Fire2:
		TNT1 A 1 A_SpawnItem("BurnDeath1",2,1)
		TNT1 A 0 A_AlertMonsters
		stop
	Death.head:
		GO99 O 9 A_PlaySound("headshot/splat")
		TNT1 A 0 A_SpawnItem("AeniBlood",0,40,0)
		TNT1 A 0 A_SpawnItem("AeniBlood",0,40,0)
		GO99 P 9 A_XScream
		GO99 Q 9 A_Fall
		GO99 RS 5
		TNT1 A 0 A_SpawnItemEx("DEADInitiate",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
		stop
	Raise:
		GO99 KJIH 5
		Goto See
	}
}

ACTOR RookKnightArtillery1 : Rocket
{
  Obituary "%o was bombarded by a Rook Knight!"
  Speed 25
  -NOGRAVITY
  +THRUGHOST
  Scale 0.5
  States
  {
  Spawn:
    SBLL ABCDEFGHIJ 1 bright
    loop
  Death:
    TNT1 A 8 bright A_CustomMissile("ZombieTankExplosion", 0, 0, 0)
    TNT1 A 8
    TNT1 A 8
    TNT1 A 8 bright A_SpawnItemEx("DustCloudClusterLarge",0,0,0,0,0,0,0,0,0)
    stop
  }
}

ACTOR RookKnightArtillery2 : RookKnightArtillery1
{
  Speed 30
}

ACTOR RookKnightArtillery3 : RookKnightArtillery1
{
  Speed 35
}

ACTOR RookKnightArtillery4 : RookKnightArtillery1
{
  Speed 40
}

ACTOR RookKnightArtillery5 : RookKnightArtillery1
{
  Speed 45
}

ACTOR RookKnightArtillery6 : RookKnightArtillery1
{
  Speed 50
}

ACTOR RookKnightArtillery7 : RookKnightArtillery1
{
  Speed 55
}

ACTOR RookKnightArtillery8 : RookKnightArtillery1
{
  Speed 60
}

actor ShieldPart
{
  Radius 16
  Height 16
  Scale 0.5
  RenderStyle None
  Alpha 0.25
  Bloodtype "ShieldHit"
  +SHOOTABLE
  +NOGRAVITY
  +NOTELEPORT
  +NODAMAGE
  +DONTRIP
  +NOBLOODDECALS
  +FLOORCLIP
  //+REFLECTIVE
  //+GHOST	//Give projectiles that you wish to pass through the shield THRUGHOST flag
  States
  {
  Spawn:
    HEXA AAA 35 bright
    stop
  Death:
    TNT1 A 2
    stop
  }
}

actor ShieldPartVisualG
{
  Radius 8
  Height 8
  Scale 0.5
  RenderStyle Add	//Fuzzy looks great in software mode
  Alpha 0.25
  +NOINTERACTION
  +CLIENTSIDEONLY
  +FLOORCLIP
  States
  {
  Spawn:
    HEXA AAA 35 bright
    stop
  Death:
    TNT1 A 2
    stop
  }
}

actor ShieldSpawner
{
  Radius 1
  Height 1
  +NOGRAVITY
  +NOINTERACTION
  States
  {
  Spawn:
    TNT1 A 0
    TNT1 A 1
    TNT1 A 0 A_SpawnItemEx ("ShieldSpawner4up", 0, 0, 0, 0, 0, 0, 0, 32)
    TNT1 A 0 A_SpawnItemEx ("ShieldSpawner5up", 24, 0, 0, 0, 0, 0, 0, 32)
    TNT1 A 0 A_SpawnItemEx ("ShieldSpawner5up", -24, 0, 0, 0, 0, 0, 0, 32)
    TNT1 A 0 A_SpawnItemEx ("ShieldSpawner4up", 48, 0, 0, 0, 0, 0, 0, 32)
    TNT1 A 0 A_SpawnItemEx ("ShieldSpawner4up", -48, 0, 0, 0, 0, 0, 0, 32)
    TNT1 A 0 A_SpawnItemEx ("ShieldSpawner3up", 72, 0, 0, 0, 0, 0, 0, 32)
    TNT1 A 0 A_SpawnItemEx ("ShieldSpawner3up", -72, 0, 0, 0, 0, 0, 0, 32)
    TNT1 A 0 A_SpawnItemEx ("ShieldSpawner2up", 96, 0, 0, 0, 0, 0, 0, 32)
    TNT1 A 0 A_SpawnItemEx ("ShieldSpawner2up", -96, 0, 0, 0, 0, 0, 0, 32)
    stop
  }
}

actor ShieldSpawner5up
{
  Radius 1
  Height 1
  +NOGRAVITY
  +NOINTERACTION
  States
  {
  Spawn:
    TNT1 A 0
    TNT1 A 1
    TNT1 A 0 A_SpawnItemEx ("ShieldPart", 0, 0, 0, 0, 0, 0, 0, 32)
    TNT1 A 0 A_SpawnItemEx ("ShieldPart", 0, 0, 16, 0, 0, 0, 0, 32)
    TNT1 A 0 A_SpawnItemEx ("ShieldPart", 0, 0, 32, 0, 0, 0, 0, 32)
    TNT1 A 0 A_SpawnItemEx ("ShieldPart", 0, 0, 48, 0, 0, 0, 0, 32)
    TNT1 A 0 A_SpawnItemEx ("ShieldPart", 0, 0, 64, 0, 0, 0, 0, 32)
    //TNT1 A 0 A_SpawnItemEx ("ShieldPartVisualG", 0, 0, 0, 0, 0, 0, 0, 32)
    TNT1 A 0 A_SpawnItemEx ("ShieldPartVisualG", 0, 0, 16, 0, 0, 0, 0, 32)
    TNT1 A 0 A_SpawnItemEx ("ShieldPartVisualG", 0, 0, 32, 0, 0, 0, 0, 32)
    TNT1 A 0 A_SpawnItemEx ("ShieldPartVisualG", 0, 0, 48, 0, 0, 0, 0, 32)
    TNT1 A 0 A_SpawnItemEx ("ShieldPartVisualG", 0, 0, 64, 0, 0, 0, 0, 32)
    stop
  }
}

actor ShieldSpawner4up
{
  Radius 1
  Height 1
  +NOGRAVITY
  +NOINTERACTION
  States
  {
  Spawn:
    TNT1 A 0
    TNT1 A 1
    TNT1 A 0 A_SpawnItemEx ("ShieldPart", 0, 0, 8, 0, 0, 0, 0, 32)
    TNT1 A 0 A_SpawnItemEx ("ShieldPart", 0, 0, 24, 0, 0, 0, 0, 32)
    TNT1 A 0 A_SpawnItemEx ("ShieldPart", 0, 0, 40, 0, 0, 0, 0, 32)
    TNT1 A 0 A_SpawnItemEx ("ShieldPart", 0, 0, 56, 0, 0, 0, 0, 32)
    //TNT1 A 0 A_SpawnItemEx ("ShieldPartVisualG", 0, 0, 8, 0, 0, 0, 0, 32)
    TNT1 A 0 A_SpawnItemEx ("ShieldPartVisualG", 0, 0, 24, 0, 0, 0, 0, 32)
    TNT1 A 0 A_SpawnItemEx ("ShieldPartVisualG", 0, 0, 40, 0, 0, 0, 0, 32)
    TNT1 A 0 A_SpawnItemEx ("ShieldPartVisualG", 0, 0, 56, 0, 0, 0, 0, 32)
    stop
  }
}

actor ShieldSpawner3up
{
  Radius 1
  Height 1
  +NOGRAVITY
  +NOINTERACTION
  States
  {
  Spawn:
    TNT1 A 0
    TNT1 A 1
    TNT1 A 0 A_SpawnItemEx ("ShieldPart", 0, 0, 16, 0, 0, 0, 0, 32)
    TNT1 A 0 A_SpawnItemEx ("ShieldPart", 0, 0, 32, 0, 0, 0, 0, 32)
    TNT1 A 0 A_SpawnItemEx ("ShieldPart", 0, 0, 48, 0, 0, 0, 0, 32)
    //TNT1 A 0 A_SpawnItemEx ("ShieldPartVisualG", 0, 0, 16, 0, 0, 0, 0, 32)
    TNT1 A 0 A_SpawnItemEx ("ShieldPartVisualG", 0, 0, 32, 0, 0, 0, 0, 32)
    TNT1 A 0 A_SpawnItemEx ("ShieldPartVisualG", 0, 0, 48, 0, 0, 0, 0, 32)
    stop
  }
}


actor ShieldSpawner2up
{
  Radius 1
  Height 1
  +NOGRAVITY
  +NOINTERACTION
  States
  {
  Spawn:
    TNT1 A 0
    TNT1 A 1
    TNT1 A 0 A_SpawnItemEx ("ShieldPart", 0, 0, 24, 0, 0, 0, 0, 32)
    TNT1 A 0 A_SpawnItemEx ("ShieldPart", 0, 0, 40, 0, 0, 0, 0, 32)
    //TNT1 A 0 A_SpawnItemEx ("ShieldPartVisualG", 0, 0, 24, 0, 0, 0, 0, 32)
    TNT1 A 0 A_SpawnItemEx ("ShieldPartVisualG", 0, 0, 40, 0, 0, 0, 0, 32)
    stop
  }
}

actor ShieldHit
{
  Radius 1
  Height 1
  Scale 0.5
  Renderstyle Add
  +NOGRAVITY
  +NOINTERACTION
  States
  {
  Spawn:
    FHIT A 0 bright
    FHIT A 1 bright A_Stop
    FHIT A 0 bright A_PlaySound ("ForceBarrier/Hit")
    FHIT BCDEFGH 1 bright
    stop
  }
}

ACTOR MiniBomb : PlasmaBall
{ 
    -NOGRAVITY
    +THRUGHOST
    Mass 5
    Height 8
    Radius 6
    Scale 0.5
    Renderstyle Normal
    Damage 10
    Speed 20
    translation "16:47=160:167", "168:191=160:167", "1:1=0:0", "64:79=5:8", "128:151=5:8", "236:239=5:8"
    SeeSound "ZSec/bounce"
    DeathSound "weapons/rocklx"
    Obituary "%o was shelled by a Rook Knight."
    States
    {
    Spawn:
      GRND A 1 A_CustomMissile("MiniBombTrail",0,0,0)
      Loop
    Death:
      TNT1 A 0 A_NoGravity
      TNT1 A 0 A_SpawnItemEx("DustCloudCluster",0,0,0,0,0,0,0,0,0)
      MISL C 3 BRIGHT A_Explode(32,128,0)
      MISL D 4 bright
      MISL E 3 bright
      Stop
    }
}

ACTOR MiniBombTrail : PlasmaBall { 
	Scale 0.25

	Renderstyle Add
	Alpha 0.95
	
	Damage 0
	Speed 0
	
	+NOCLIP
	+CLIENTSIDEONLY
	
	SeeSound ""
	DeathSound ""
	
	States {
		Spawn:
			SMOK A 17 BRIGHT
			Stop
	}
}
