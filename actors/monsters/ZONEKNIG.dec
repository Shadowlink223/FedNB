ACTOR ZoneKnight : Enemy 12487 {
	//$Category Federation

	Health 225
	GibHealth 60
	Radius 16
	Height 40
	Speed 18
	Mass 100

	+MISSILEMORE
	+FASTER

	DropItem "Clip" 128
	DropItem "Clip" 64
	DropItem "RocketAmmo" 16

	Dropitem "Stimpack" 32
	Dropitem "ZoneArmor" 4
	DropItem "ArmorShardSpawner" 64

	DropItem "Chaingun" 4

	//Dropitem "PDA" 4

	Dropitem "0Beacon" 2

	Translation "112:127=240:247"

	BloodType "AeniBlood"
	Decal "BulletChip"
	
	DamageFactor "DU", 1.1
	DamageFactor "HP", 1.25
	DamageFactor "Trid", 1.5

	PainChance 100
	PainChance "Shock", 255
	PainChance "ShockSlug", 255

	SeeSound "Infantry/sight"
	AttackSound ""
	PainSound "Infantry/pain"
	DeathSound "Infantry/death"
	ActiveSound "Infantry/active"

	Obituary "%o trespassed in a Zone Knight's territory."

	States {
	
/******************************************************************************\
==================================MAIN STATES===================================
\******************************************************************************/
	
	Spawn:
		XXLF A 0
		XXLF A 0 Thing_SetSpecial(0,226,642,0,random(10,20))
	SpawnLoop:
		TNT1 A 0 A_SpawnItem("HeadshotTarget10",15,40,0)
		XXLF A 10 A_LookEx(0,0,random(512,2048),random(1024,4096),random(150,120),"See")
		goto SpawnLoop
	See:
		TNT1 A 0 A_Jump(230,"Stand")
		TNT1 A 0 A_SpawnItem("HeadshotTarget3",15,40,0)
		XXLF AA 3 A_Chase
		TNT1 A 0 A_SpawnItem("HeadshotTarget3",15,40,0)
		XXLF BB 3 A_Chase
		TNT1 A 0 A_SpawnItem("HeadshotTarget3",15,40,0)
		XXLF CC 3 A_Chase
		TNT1 A 0 A_SpawnItem("HeadshotTarget3",15,40,0)
		XXLF DD 3 A_Chase
		Loop
	Stand:
		TNT1 A 0 A_Chase("","Missile",CHF_DONTMOVE)
		TNT1 A 0 A_SpawnItem("HeadshotTarget4",15,40,0)
		XXLF A 4 A_FaceTarget
		Goto See

/******************************************************************************\
=================================ATTACK STATES==================================
\******************************************************************************/

	Missile:
		TNT1 A 0 A_SpawnItem("HeadshotTarget10",0,40,0)
		XXLF E 10 A_FaceTarget
		TNT1 A 0 A_Jump(64,"DistanceCheck")
		Goto BulletAttack
	BulletAttack:
		TNT1 A 0 A_JumpIfInventory("KnightAmmo",30,"Reload")
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("FNS_F1",6)
		TNT1 A 0 A_GiveInventory("KnightAmmo",1)
		TNT1 A 0 A_SpawnItem("HeadshotTarget1",0,40,0)
		//TNT1 A 0 A_CustomBulletAttack(5,5,1,random(2,5),BulletPuff)
		XXLF F 1 BRIGHT A_CustomMissile("ZoneTracer",38,0,random(-5,5),0,random(-5,5))
		XXLF E 2 A_SpawnItem ("HeadshotTarget2", 0, 40,0)
		TNT1 A 0 A_JumpIfInventory("KnightAmmo",30,"Reload")
		TNT1 A 0 A_MonsterRefire(224,"See")
		Loop
	Reload:
		TNT1 A 0 A_ChangeFlag(NOPAIN,1)
		TNT1 A 0 A_TakeInventory("KnightAmmo",30)
		XXLF EE 3 A_SpawnItem("HeadshotTarget3",0,40,0)
		TNT1 A 0 A_PlaySound("FNS_R2")
		XXLF E 2 A_SpawnItem("HeadshotTarget2",0,40,0)
		XXLF EEEEEEEEEEEEEEEEEEEEEEE 4 A_SpawnItem("HeadshotTarget4",0,40,0)
		TNT1 A 0 A_ChangeFlag(NOPAIN,0)
		Goto See
	DistanceCheck:
		TNT1 A 0 A_JumpIfCloser(1768,"LaunchGrenade")
		TNT1 A 0 A_TakeInventory("DealWithIt",1)
		Goto BulletAttack
	LaunchGrenade:
		TNT1 A 0 A_SpawnItem("HeadshotTarget4",0,40,0)
		XXLF E 4 A_FaceTarget
		XXLF E 4 A_SpawnItem("HeadshotTarget4",0,40,0)
		TNT1 A 0 A_SpawnItem("HeadshotTarget2",0,40,0)
		TNT1 A 0 A_Jump(16,"LaunchSmokeGrenade")
		Goto LaunchHEGrenade
	LaunchHEGrenade:
		XXLF F 2 BRIGHT A_CustomMissile("KnightGrenadeShell",28,5,random(-15,15),2,random(0,10))
		Goto LaunchGrenadeFinish
	LaunchSmokeGrenade:
		XXLF F 2 A_CustomMissile("KnightSmokeGrenade",28,5,0)
		Goto LaunchGrenadeFinish
	LaunchGrenadeFinish:
		XXLF EE 3 A_SpawnItem("HeadshotTarget3",0,40,0)
		Goto GrenadeReload
	GrenadeReload:
		TNT1 A 0 A_ChangeFlag(NOPAIN,1)
		TNT1 A 0 A_PlaySoundEx("M4_CLIPO","SoundSlot5")
		XXLF E 2 A_SpawnItem("HeadshotTarget2",0,40,0)
		XXLF E 10 A_SpawnItem("HeadshotTarget10",0,40,0)
		TNT1 A 0 A_PlaySoundEx("M4_CLIPI","SoundSlot5")
		XXLF E 2 A_SpawnItem("HeadshotTarget2",0,40,0)
		XXLF EEEEEE 10 A_SpawnItem("HeadshotTarget10",0,40,0)
		TNT1 A 0 A_ChangeFlag(NOPAIN,0)
		Goto See

/******************************************************************************\
==================================PAIN STATES===================================
\******************************************************************************/

	Pain: 
		XXLF G 3 A_SpawnItem("HeadshotTarget3",0,40,0)
		TNT1 A 0 A_SpawnItem("HeadshotTarget3",0,40,0)
		XXLF G 3 A_Pain
		Goto See
	Pain.Shock:
	Pain.ShockSlug:
		TNT1 A 0 A_ChangeFlag(NOPAIN,1)
		TNT1 A 0 A_Pain
		XXLF GGGGGGGGGGGGGGGGGGGGGGGGGGGGGG 10 A_SpawnItem("HeadshotTarget10",0,40,0)
		TNT1 A 0 A_ChangeFlag(NOPAIN,0)
		Goto See
	Pain.KillMe:
		Goto See

/******************************************************************************\
==================================DEATH STATES==================================
\******************************************************************************/

	Death: 
		TNT1 A 0 A_JumpIfHealthLower(-60,"Splode")
		XXLF H 5 A_AlertMonsters
		XXLF I 5 A_Scream
		Goto DeathFall
	Death.KnifeSlash:
	Death.KnifeStab:
	Death.KnifeSlashPower:
	Death.KnifeStabPower:
	Death.WakizashiSlash:
	Death.WakizashiStab:
	Death.WakizashiSlashPower:
	Death.WakizashiStabPower:
		XXLF H 5
		XXLF I 5 A_PlaySound("KNI_HIT")
	DeathFall:
		XXLF J 5 A_Fall
		XXLF KLM 5
		TNT1 A 0 A_Jump(2,"Regenerating")
		XXLF N -1
		stop
	Regenerating:
		XXLF NNNNNNNNNNNNNNN 70
		XXLF N -1 Thing_Raise(0)
		Wait
	Splode:
		TNT1 A 0 A_XScream
		TNT1 A 0 A_NoBlocking
		XXLF O 5 A_SpawnItem("AeniBlood",0,32,0)
		TNT1 A 0 A_SpawnItemEx ("AeniBlood", 0,34,0, random(-75,75)*0.01,random(-75,75)*0.01,random(-65,65)*0.01, 0, SXF_NOCHECKPOSITION|SXF_CLIENTSIDE|SXF_TRANSFERTRANSLATION, 0)	
		XXLF P 5 A_SpawnItem("AeniBlood",0,34,0)
		XXLF Q 5 A_SpawnItem("AeniBlood",0,26,0)
		XXLF R 5 A_SpawnItem("AeniBlood",0,20,0)
		XXLR S 5 A_SpawnItem("AeniBlood",0,14,0)
		XXLR T 5 A_SpawnItem("AeniBlood",0,6,0)
		XXLR U 5 A_SpawnItem("AeniBlood",0,0,0)
		XXLR V 5 A_SpawnItem("AeniBlood",0,0,0)
		XXLR W 5 // No extra blood needed here. He's dead, Jim.
		TNT1 A 0 A_SpawnItemEx("DEADStrikeKnightGib",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
		Stop
	Death.Fire2:
		TNT1 A 1 A_SpawnItem("BurnDeath1",2,1)
		TNT1 A 0 A_AlertMonsters
		stop
	Death.head:
		GO99 O 9 A_PlaySound("headshot/splat")
		TNT1 A 0 A_SpawnItem("AeniBlood",0,40,0)
		TNT1 A 0 A_SpawnItem("AeniBlood",0,40,0)
		GO99 P 9 A_XScream
		GO99 Q 9 A_Fall
		GO99 R 5
		GO99 S 5 A_SpawnItemEx("DEADZoneKnight",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
		stop
	Raise: 
		XXLF KJI 5
		XXLF H 5 Thing_SetSpecial(0,226,642,0,random(10,20))
		Goto See
	}
}

ACTOR DEADZoneKnight {
	+FLOORCLIP
	Radius 12
	Height 11
	States {
		Spawn:
			GO99 T 0
			GO99 T -1
			stop
	}
}

ACTOR DEADZoneKnightHead {
	+FLOORCLIP
	Radius 12
	Height 11
	States {
		Spawn:
			XXLF W 0
			XXLF W -1
			stop
	}
}

ACTOR DEADZoneKnightGib {
	+FLOORCLIP
	Radius 12
	Height 11
	States {
		Spawn:
			XXLR W 0
			XXLR W -1
			stop
	}
}