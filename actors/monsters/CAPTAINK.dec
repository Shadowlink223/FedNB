ACTOR KnightCaptain : RandomSpawner 13205 {
	DropItem KnightCaptain_MP7 255 3
	DropItem KnightCaptain_Deagle 255 1
}

Actor KnightCaptain_MP7 : Enemy 17056 {
	//$Category Federation
	
	Health 540
	GibHealth 60
	Radius 16
	Height 40
	Speed 21
	Mass 100

	MeleeDamage 8

	+FASTER
	+MISSILEMORE

	DropItem "Clip" 128
	DropItem "Clip" 128
	DropItem "Clip" 64
	DropItem "RocketAmmo" 16
	DropItem "RocketAmmo" 8

	Dropitem "Stimpack" 64
	Dropitem "Medikit" 32
	DropItem "ArmorShardSpawner" 64

	DropItem "Chaingun" 4
	DropItem "Chaingun" 1
	
	//Dropitem "PDA" 8

	DropItem "2Beacon" 32
	Dropitem "7Beacon" 64
	DropItem "MortarStrikeItem" 32

	Translation "112:127=160:167"

	BloodType "AeniBlood"
	Decal "BulletChip"
	
	DamageFactor "DU", 1.1
	DamageFactor "HP", 1.25
	DamageFactor "Trid", 1.5

	PainChance 25
	DamageFactor "head", 0.35
    //PainChance "head", 0.5
	PainChance "Shock", 128
	PainChance "ShockSlug", 255
	DamageFactor "WakizashiSlashPower", 0.3
	DamageFactor "WakizashiStabPower", 0.3
	
	SeeSound "Knight/CaptainSight"
	AttackSound ""
	PainSound "Knight/KnightPain"
	DeathSound "monster/inidth"
	ActiveSound "monster/iniact"

	Obituary "%o was executed by a Knight Captain."
	Hitobituary "%o was crushed by a Knight Captain."

	States {
	
/******************************************************************************\
================================HEADSHOT STATES=================================
\******************************************************************************/
	
	Spawn:
		GO99 A 0
		TNT1 A 0 A_GiveInventory("MortarToken",1)
		TNT1 A 0 Thing_SetSpecial(0,226,642,0,random(60,80))
	SpawnLoop:
		TNT1 A 0 A_SpawnItem("HeadshotTarget4",15,40,0)
		GO99 A 6 A_LookEx(0,0,random(1024,2048),random(2048,4096),random(90,120),"SeeAlert")
		TNT1 A 0 HealThing(2)
		Loop
	SeeAlert:
		GO99 A 1 A_AlertMonsters
		Goto See
	See:
		GO99 A 1 // Infinite loop crash prevention?
		TNT1 A 0 A_Jump(32,"Stand")
		Goto IfLowHealth
	IfLowHealth:
		TNT1 A 0 A_JumpIfHealthLower(120,"LowHealth")
		TNT1 A 0 A_TakeInventory("DealWithIt",1)
		Goto HealthHigh
	HealthHigh:
		TNT1 A 0 A_ChangeFlag("Frightened",0)
		Goto Chase
	LowHealth:
		TNT1 A 0 A_JumpIfInventory("Reinforce",1,"Reinforcements")
		TNT1 A 0 A_TakeInventory("DealWithIt",1)
		Goto Chase
	Retreat:
		GO99 A 1 A_AlertMonsters
		TNT1 A 0 A_ChangeFlag("Frightened",1)
		Goto Chase
	Chase:
		TNT1 A 0 HealThing(2)
		TNT1 A 0 A_SpawnItem("HeadshotTarget3",15,40,0)
		GO99 AA 3 A_FastChase
		TNT1 A 0 A_SpawnItem("HeadshotTarget3",15,40,0)
		GO99 BB 3 A_Chase("Melee","",CHF_NOPLAYACTIVE)
		TNT1 A 0 A_SpawnItem("HeadshotTarget3",15,40,0)
		GO99 CC 3 A_Chase("Melee","",CHF_NOPLAYACTIVE)
		TNT1 A 0 A_SpawnItem("HeadshotTarget3",15,40,0)
		GO99 DD 3 A_Chase("Melee","",CHF_NOPLAYACTIVE)
		Goto See
	Stand:
		TNT1 A 0 A_Chase("Melee","Missile",CHF_NOPLAYACTIVE|CHF_DONTMOVE)
		TNT1 A 0 A_SpawnItem("HeadshotTarget4",15,40,0)
		GO99 A 4 A_FaceTarget
		TNT1 A 0 HealThing(2)
		TNT1 A 0 A_Jump(64,"See")
		Goto Stand
		
/******************************************************************************\
=================================ATTACK STATES==================================
\******************************************************************************/
		
	Melee:
		TNT1 A 0 A_FaceTarget
		GO99 E 3 A_CustomMeleeAttack(8,"monster/inipunc1","none")
		TNT1 A 0 A_FaceTarget
		GO99 A 3 A_CustomMissile("Knockback",32,0,0,0)
		Goto See
	Missile:
		TNT1 A 0 A_ChangeFlag("NoPain",1)
		GO99 E 2 A_FaceTarget
		TNT1 A 0 A_Jump(64,"DistanceCheck")
		Goto BulletAttack
	BulletAttack:
		TNT1 A 0 A_JumpIfInventory("KnightAmmo",40,"Reload")
		TNT1 A 0 A_SpawnItem("HeadshotTarget10",0,40,0)
		//TNT1 A 0 A_CustomBulletAttack(5,5,1,random(1,3),"BulletPuff")
		TNT1 A 0 A_PlaySoundEx("MP7/FIRE","SoundSlot6")
		GO99 F 2 BRIGHT A_CustomMissile("CaptainTracer",38,0,random(-5,5),0,random(-5,5))
		TNT1 A 0 A_SpawnItem("HeadshotTarget10",0,40,0)
		GO99 E 2 A_FaceTarget
		TNT1 A 0 A_GiveInventory("KnightAmmo",1)
		TNT1 A 0 A_JumpIfInventory("KnightAmmo",40,"Reload")
		TNT1 A 0 A_Jump(8,"StopShooting")
		TNT1 A 0 A_SpidReFire
		Loop
	StopShooting:
		TNT1 A 0 A_JumpIfInventory("KnightAmmo",40,"Reload")
		TNT1 A 0 A_TakeInventory("DealWithIt",1)
		goto See
	Reload:
		TNT1 A 0 A_TakeInventory("KnightAmmo",40)
		GO99 E 8 A_PlaySound("ZSec/DryFire")
		GO99 E 55 A_PlaySound("ZSec/Reload")
		TNT1 A 0 A_ChangeFlag("NoPain",0)
		goto See
	DistanceCheck:
		TNT1 A 0 A_JumpIfCloser(1024,"ThrowGrenade")
		TNT1 A 0 A_JumpIfInventory("MortarToken",1,"CheckMortar")
		Goto See
	ThrowGrenade:
		TNT1 A 0 A_Jump(16,"SmokeGrenade")
		GO99 E 8 A_FaceTarget
		TNT1 A 0 A_PlaySound("GRE_THRO",CHAN_AUTO)
		GO99 F 2 bright A_CustomMissile("ThrownGrenade",28,5,random(-15,15),0)
		GO99 E 6 A_FaceTarget
		TNT1 A 0 A_ChangeFlag("NoPain",0)
		Goto See
	SmokeGrenade:
		GO99 E 8 A_FaceTarget
		TNT1 A 0 A_PlaySound("weapons/rocklf")
		GO99 F 2 bright A_CustomMissile("KnightSmokeGrenade",28,5,0)
		GO99 E 6 A_FaceTarget
		TNT1 A 0 A_ChangeFlag("NoPain",0)
		Goto See
	CheckMortar:
		TNT1 A 0 A_Jump(64,"MortarStrike")
		Goto See
	MortarStrike:
		GO99 E 3 A_FaceTarget
		TNT1 A 0 A_TakeInventory("MortarToken",1)
		TNT1 A 0 A_PlaySoundEx("Artillery/FireMission","Auto")
		TNT1 A 0 A_CustomMissile("MortarSpawner",40,0,0,0,0)
		TNT1 A 0 A_CustomMissile("MortarSpawner",40,0,0,0,0)
		TNT1 A 0 A_CustomMissile("MortarSpawner",40,0,0,0,0)
		TNT1 A 0 A_CustomMissile("MortarSpawner",40,0,0,0,0)
		TNT1 A 0 A_CustomMissile("MortarSpawner",40,0,0,0,0)
		TNT1 A 0 A_CustomMissile("MortarSpawner",40,0,0,0,0)
		TNT1 A 0 A_CustomMissile("MortarSpawner",40,0,0,0,0)
		TNT1 A 0 A_CustomMissile("MortarSpawner",40,0,0,0,0)
		TNT1 A 0 A_CustomMissile("MortarSpawner",40,0,0,0,0)
		TNT1 A 0 A_CustomMissile("MortarSpawner",40,0,0,0,0)
		GO99 E 140
		Goto See
		
/******************************************************************************\
==================================PAIN STATES===================================
\******************************************************************************/
		
	Pain:
		GO99 G 3
		GO99 G 3 A_Pain
		TNT1 A 0 HealThing(2)
		TNT1 A 0 A_GiveInventory("Reinforce",1)
		Goto See
	Pain.Shock:
	Pain.ShockSlug:
		GO99 G 2 A_ChangeFlag("NoPain",1)
		GO99 G 300 A_Pain
		TNT1 A 0 A_ChangeFlag("NoPain",0)
		Goto See
	Pain.KillMe:
		Goto See
		
/******************************************************************************\
==================================DEATH STATES==================================
\******************************************************************************/
		
	Death:
		TNT1 A 0 A_JumpIfHealthLower(-60,"Splode")
		GO99 H 5 A_AlertMonsters
		GO99 I 5 A_Scream
		Goto DeathFall
	Death.KnifeSlash:
	Death.KnifeStab:
	Death.KnifeSlashPower:
	Death.KnifeStabPower:
	Death.WakizashiSlash:
	Death.WakizashiStab:
	Death.WakizashiSlashPower:
	Death.WakizashiStabPower:
		GO99 H 5
		GO99 I 5 A_PlaySound("KNI_HIT")
	DeathFall:
		GO99 J 5 A_Fall
		GO99 KLM 5
		TNT1 A 0 A_Jump(64,"Regenerating")
		GO99 N -1
		stop
	Regenerating:
		GO99 NNNNN 70//
		GO99 NNNNN 70//
		GO99 NNNNN 70
		GO99 N -1 Thing_Raise(0)
		Wait
	Splode:
		GO99 I 1
		TNT1 A 0 A_XScream
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_SpawnDebris("NashGore_Gib9",0)
		TNT1 A 0 A_SpawnDebris("NashGore_Gib10",0)
		TNT1 A 0 A_SpawnDebris("NashGore_Gib11",0)
		TNT1 A 0 A_SpawnDebris("NashGore_Gib11",0)
		TNT1 A 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,0)
		TNT1 A 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,0)
		TNT1 A 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,0)
		TNT1 A 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,0)
		TNT1 A 0 A_SpawnItemEx("DEADInitiateHead",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
		stop
	Death.Fire2:
		TNT1 A 1 A_SpawnItem("BurnDeath1",2,1)
		stop
	Raise:
		GO99 KJI 5
		GO99 H 5 Thing_SetSpecial(0,226,642,0,random(60,80))
		Goto See
	Death.head:
		GO99 O 9 A_PlaySound("headshot/splat")
		TNT1 A 0 A_SpawnItem("AeniBlood",0,40,0)
		TNT1 A 0 A_SpawnItem("AeniBlood",0,40,0)
		GO99 P 9 A_XScream
		GO99 Q 9 A_Fall
		GO99 RS 5
		TNT1 A 0 A_SpawnItemEx("DEADInitiate",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
		stop
	Reinforcements:
		GO99 E 1 A_TakeInventory("Reinforce",1)
		TNT1 A 0 A_Jump(16,2)
		GO99 E 1 A_SpawnItemEx("WeakBeacon",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		Goto See
		GO99 E 1 A_SpawnItemEx("StrongBeacon",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		Goto See
	}
}

Actor KnightCaptain_Deagle : KnightCaptain_MP7 17057 {
	//$Category Federation
	Speed 21
	AttackSound ""

	DropItem "Clip" 256
	DropItem "Clip" 256
	DropItem "Clip" 128
	DropItem "RocketAmmo" 32
	DropItem "RocketAmmo" 16

	Dropitem "Stimpack" 64
	Dropitem "Medikit" 32
	Dropitem "CaptainArmor" 4
	DropItem "ArmorShardSpawner" 64

	//Dropitem "PDA" 8

	DropItem "2Beacon" 32
	Dropitem "7Beacon" 64
	DropItem "MortarStrikeItem" 32

	States {
		Missile:
			TNT1 A 0 A_ChangeFlag("NoPain",1)
			GO99 E 2 A_FaceTarget
			TNT1 A 0 A_Jump(64,"DistanceCheck")
			Goto BulletAttack
		BulletAttack:
			TNT1 A 0 A_JumpIfInventory("KnightAmmo",7,"Reload")
			TNT1 A 0 A_CustomBulletAttack(5,5,1,random(6,12),BulletPuff)
			TNT1 A 0 A_PlaySoundEx("DE_FIRE","Auto")
			GO99 F 2 BRIGHT A_CustomMissile("CaptainTracer",38,0,random(-6,6),2,random(-6,6))
			GO99 E 28 A_GiveInventory("KnightAmmo",1)
			TNT1 A 0 A_JumpIfInventory("KnightAmmo",7,"Reload")
			TNT1 A 0 A_Jump(32,"StopShooting")
			TNT1 A 0 A_SpidReFire
			Goto BulletAttack
		StopShooting:
			TNT1 A 0 A_JumpIfInventory("KnightAmmo",7,"Reload")
			goto See
		Reload:
			TNT1 A 0 A_TakeInventory("KnightAmmo",7)
			GO99 E 8 A_PlaySound("DE_R4")
			GO99 E 24 A_PlaySound("DE_R1")
			GO99 E 40 A_PlaySound("DE_R2")
			GO99 E 24 A_PlaySound("DE_R3")
			GO99 E 86 A_PlaySound("DE_R4")
			TNT1 A 0 A_ChangeFlag("NoPain",0)
			goto See
	}
}

ACTOR Knockback {
	Height 4
	Speed 50
	Radius 50 
	Height 16
	PROJECTILE
	+RANDOMIZE
	states {
		Spawn:  
			TNT1 A 0 A_PlaySound("monster/inipunc2")
			TNT1 A 10
			loop
		Death:
			TNT1 A 6 A_RadiusThrust(5000,32,0)
			stop
	}
}

ACTOR WeakBeacon {
	-SOLID
	-SHOOTABLE
	+NOTARGET
	States {
		Spawn:
			7BEA A 105 BRIGHT
			TNT1 A 0 A_CustomMissile ("BeaconTeleportFog", 0, 0, 0, 0, 0)
			TNT1 A 0 A_CustomMissile ("BeaconTeleportFog", 0, 32, 0, 0, 0)
			TNT1 A 0 A_CustomMissile ("BeaconTeleportFog", 0, 32, 64, 0, 0)
			TNT1 A 0 A_CustomMissile ("BeaconTeleportFog", 0, 32, 128, 0, 0)
			TNT1 A 0 A_CustomMissile ("BeaconTeleportFog", 0, 32, 192, 0, 0)
			TNT1 A 0 A_SpawnItemEx ("StrikeKnight", 0, 0, 8, 0, 0, 0, 0, 1)
			TNT1 A 0 A_SpawnItemEx ("StrikeKnight", 40, 0, 8, 0, 0, 0, 0, 1)
			TNT1 A 0 A_SpawnItemEx ("StrikeKnight", -40, 0, 8, 0, 0, 0, 0, 1)
			TNT1 A 0 A_SpawnItemEx ("StrikeKnight", 0, 40, 8, 0, 0, 0, 0, 1)
			TNT1 A 0 A_SpawnItemEx ("StrikeKnight", 0, -40, 8, 0, 0, 0, 0, 1)
			7BEA A 105 BRIGHT
			goto Death
		Death:
			TNT1 A 1 A_CustomMissile ("BeaconTeleportFog", 0, 0, 0, 0, 0)
			stop
	}
}
  
ACTOR StrongBeacon {
	-SOLID
	-SHOOTABLE
	+NOTARGET
	States {
		Spawn:
			2BEA A 105 BRIGHT
			TNT1 A 0 A_Jump(128,"SpawnDeagle")
			TNT1 A 0 A_SpawnItemEx ("KnightCaptain_MP7", 0, 0, 8, 0, 0, 0, 0, 1)
			Goto SpawnRest
		SpawnDeagle:
			TNT1 A 0 A_SpawnItemEx ("KnightCaptain_Deagle", 0, 0, 8, 0, 0, 0, 0, 1)
		SpawnRest:
			TNT1 A 0 A_CustomMissile ("BeaconTeleportFog", 0, 0, 0, 0, 0)
			TNT1 A 0 A_CustomMissile ("BeaconTeleportFog", 0, 32, 0, 0, 0)
			TNT1 A 0 A_CustomMissile ("BeaconTeleportFog", 0, 32, 64, 0, 0)
			TNT1 A 0 A_CustomMissile ("BeaconTeleportFog", 0, 32, 128, 0, 0)
			TNT1 A 0 A_CustomMissile ("BeaconTeleportFog", 0, 32, 192, 0, 0)
			TNT1 A 0 A_SpawnItemEx ("StrikeKnight", 40, 0, 8, 0, 0, 0, 0, 1)
			TNT1 A 0 A_SpawnItemEx ("StrikeKnight", -40, 0, 8, 0, 0, 0, 0, 1)
			TNT1 A 0 A_SpawnItemEx ("StrikeKnight", 0, 40, 8, 0, 0, 0, 0, 1)
			TNT1 A 0 A_SpawnItemEx ("HeavyKnight", 0, -40, 8, 0, 0, 0, 0, 1)
			2BEA A 105 BRIGHT
			goto Death
		Death:
			TNT1 A 1 A_CustomMissile ("BeaconTeleportFog", 0, 0, 0, 0, 0)
			stop
	}
}


Actor CaptainScared : Inventory { Inventory.MaxAmount 1 }
Actor Reinforce : Inventory { Inventory.MaxAmount 1 }
actor MortarToken : inventory { Inventory.MaxAmount 1 }