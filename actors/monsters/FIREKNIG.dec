ACTOR FireKnight : Enemy 13969 {
	//$Category Federation

	Health 225
	GibHealth 60
	Radius 16
	Height 40
	Mass 100
	Speed 20

	+MISSILEMORE
	+FASTER

	DropItem "Clip" 128
	DropItem "Clip" 64
	DropItem "Cell" 64
   	DropItem "CellPack" 16
   	DropItem "RocketAmmo" 16

	DropItem "Stimpack" 32
	DropItem "FireArmor" 4
	DropItem "ArmorShardSpawner" 64

	DropItem "Chaingun" 4
	DropItem "PlasmaRifle" 1

	//Dropitem "PDA" 4

	Dropitem "BBeacon" 4

	Translation "112:127=208:223"

	BloodType "AeniBlood"
	Decal "BulletChip"
	
	DamageFactor "DU", 1.1
	DamageFactor "HP", 1.25
	DamageFactor "Trid", 1.5

	PainChance 50
	PainChance "Shock", 255
	PainChance "ShockSlug", 255
	DamageFactor "Fire2", 0.05
  	PainChance "Fire2", 0

	SeeSound "Knight/KnightSight"
	AttackSound ""
	PainSound "Knight/KnightPain"
	DeathSound "monster/inidth"
	ActiveSound "monster/iniact"

	Obituary "%o was toasted by a Fire Knight." 

	States {

/******************************************************************************\
==================================MAIN STATES===================================
\******************************************************************************/

	Spawn:
		SKFA A 0
		TNT1 A 0 Thing_SetSpecial(0,226,642,0,random(14,28))
	SpawnLoop:
		TNT1 A 0 A_SpawnItem("HeadshotTarget10f",15,40,0)
		SKFA E 10 A_LookEx(0,0,random(512,2048),random(1024,4096),random(90,120),"See")
		Loop
	See:
		SKFA A 1 // Infinite loop crash prevention?
		TNT1 A 0 A_CheckSight("ChanceToCharge")
		Goto TakeCharge
	ChanceToCharge:
		TNT1 A 0 A_Jump(128,"GiveCharge")
		TNT1 A 0 A_TakeInventory("DealWithIt",1)
		Goto Chase
	GiveCharge:
		TNT1 A 0 A_JumpIfInventory("KnightCharge",1,"TakeCharge")
		TNT1 A 0 A_GiveInventory("KnightCharge",1)
		Goto IfLowHealth
	TakeCharge:
		TNT1 A 0 A_TakeInventory("KnightCharge",1)
		Goto IfLowHealth
	IfLowHealth:
		TNT1 A 0 A_JumpIfHealthLower(50,"LowHealth")
		TNT1 A 0 A_TakeInventory("DealWithIt",1)
		Goto HealthHigh
	HealthHigh:
		TNT1 A 0 A_ChangeFlag("Frightened",0)
		Goto Chase
	LowHealth:
		TNT1 A 0 A_Jump(32,"HealUp")
		TNT1 A 0 A_TakeInventory("DealWithIt",1)
		Goto Retreat
	Retreat:
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_ChangeFlag("Frightened",1)
		Goto HealUp
	HealUp:
		TNT1 A 0 HealThing(1)
		Goto Chase
	Chase:
		TNT1 A 0 A_JumpIfInventory("KnightCharge",1,"Charge")
		TNT1 A 0 A_SpawnItem("HeadshotTarget3f",15,40,0)
		SKFA AA 3 A_FastChase
		TNT1 A 0 A_SpawnItem("HeadshotTarget3f",15,40,0)
		SKFA BB 3 A_Chase
		TNT1 A 0 A_SpawnItem("HeadshotTarget3f",15,40,0)
		SKFA CC 3 A_Chase
		TNT1 A 0 A_SpawnItem("HeadshotTarget3f",15,40,0)
		SKFA DD 3 A_Chase
		Goto See
	Charge:
		TNT1 A 0 A_SpawnItem("HeadshotTarget3f",15,40,0)
		SKFA A 3 A_Chase
		TNT1 A 0 A_SpawnItem("HeadshotTarget3f",15,40,0)
		SKFA B 3 A_Chase
		TNT1 A 0 A_SpawnItem("HeadshotTarget3f",15,40,0)
		SKFA C 3 A_Chase
		TNT1 A 0 A_SpawnItem("HeadshotTarget3f",15,40,0)
		SKFA D 3 A_Chase
		Goto See

/******************************************************************************\
=================================ATTACK STATES==================================
\******************************************************************************/

	Missile:
		TNT1 A 0 A_SpawnItem("HeadshotTarget10f",0,40,0)
		SKFA E 10 A_FaceTarget
		TNT1 A 0 A_Jump(64,"DistanceCheck")
		Goto BulletAttack
	BulletAttack:
		TNT1 A 0 A_JumpIfInventory("KnightAmmo",30,"Reload")
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("FNS_F1",6)
		TNT1 A 0 A_GiveInventory("KnightAmmo",1)
		TNT1 A 0 A_SpawnItem("HeadshotTarget1f",0,40,0)
		//TNT1 A 0 A_CustomBulletAttack(5,5,1,random(1,3),BulletPuff)
		SKFA F 1 BRIGHT A_CustomMissile("FireTracer",38,0,random(-5,5),0,random(-5,5))
		SKFA E 2 A_SpawnItem("HeadshotTarget2f",0,40,0)
		TNT1 A 0 A_JumpIfInventory("KnightAmmo",40,"Reload")
		TNT1 A 0 A_MonsterRefire(224,"See")
		Loop
	Reload:
		TNT1 A 0 A_ChangeFlag(NOPAIN,1)
		TNT1 A 0 A_TakeInventory("KnightAmmo",30)
		SKFA EE 3 A_SpawnItem("HeadshotTarget3f",0,40,0)
		TNT1 A 0 A_PlaySound("FNS_R2")
		SKFA E 2 A_SpawnItem("HeadshotTarget2f",0,40,0)
		SKFA EEEEEEEEEEEEEEEEEEEEEEE 4 A_SpawnItem("HeadshotTarget4f",0,40,0)
		TNT1 A 0 A_ChangeFlag(NOPAIN,0)
		Goto See
	DistanceCheck:
		TNT1 A 0 A_JumpIfCloser(1768,"CloseRangeSpecial")
		TNT1 A 0 A_TakeInventory("DealWithIt",1)
		Goto BulletAttack
	CloseRangeSpecial:
		TNT1 A 0 A_SpawnItem("HeadshotTarget4f",0,40,0)
		SKFA E 4 A_FaceTarget
		TNT1 A 0 A_JumpIfCloser(192,"KnifeLunge")
		Goto LaunchFireGrenade
	LaunchFireGrenade:
		SKFA F 2 BRIGHT A_CustomMissile("KnightFireGrenadeShell",28,5,random(-15,15),2,random(0,10))
		Goto LaunchGrenadeFinish
	LaunchGrenadeFinish:
		SKFA EE 3 A_SpawnItem("HeadshotTarget3f",0,40,0)
		Goto GrenadeReload
	GrenadeReload:
		TNT1 A 0 A_ChangeFlag(NOPAIN,1)
		TNT1 A 0 A_PlaySoundEx("M4_CLIPO","SoundSlot5")
		SKFA E 2 A_SpawnItem("HeadshotTarget2f",0,40,0)
		SKFA E 10 A_SpawnItem("HeadshotTarget10f",0,40,0)
		TNT1 A 0 A_PlaySoundEx("M4_CLIPI","SoundSlot5")
		SKFA E 2 A_SpawnItem("HeadshotTarget2f",0,40,0)
		SKFA EEEEEE 10 A_SpawnItem("HeadshotTarget10f",0,40,0)
		TNT1 A 0 A_ChangeFlag(NOPAIN,0)
		Goto See
	KnifeLunge:
		TNT1 A 0 ThrustThing(angle*256/360,25,0,0)
		TNT1 A 0 A_SpawnItem("HeadshotTarget4f",0,40,0)
		SKFA E 4 A_FaceTarget
		TNT1 A 0 A_PlaySound("knife/swing")
		TNT1 A 0 A_SpawnItem("HeadshotTarget2f",0,40,0)
		SKFA E 2 A_CustomMeleeAttack(34,"knife/swing","knife/swing","Knife",1)
		Goto CloseRangeFinish
	CloseRangeFinish:
		SKFA EE 3 A_SpawnItem("HeadshotTarget3f",0,40,0)
		Goto See

/******************************************************************************\
==================================PAIN STATES===================================
\******************************************************************************/

	Pain:
		SKFA G 3 A_SpawnItem("HeadshotTarget4f",0,40,0)
		TNT1 A 0 A_SpawnItem("HeadshotTarget4f",0,40,0)
		SKFA G 3 A_Pain
		Goto See
	Pain.Shock:
	Pain.ShockSlug:
		TNT1 A 0 A_ChangeFlag(NOPAIN,1)
		TNT1 A 0 A_Pain
		SKFA GGGGGGGGGGGGGGGGGGGGGGGGGGGGGG 10 A_SpawnItem("HeadshotTarget10f",0,40,0)
		TNT1 A 0 A_ChangeFlag(NOPAIN,0)
		Goto See
	Pain.KillMe:
		Goto See

/******************************************************************************\
==================================DEATH STATES==================================
\******************************************************************************/

	Death:
		TNT1 A 0 A_JumpIfHealthLower(-60,"Splode")
		SKFA H 5 A_AlertMonsters
		SKFA I 5 A_Scream
		TNT1 A 0 A_CustomMissile("FKFireDie",5,0,90)
		TNT1 A 0 A_CustomMissile("FKFireDie",5,0,45)
		TNT1 A 0 A_CustomMissile("FKFireDie",5,0,-90)
		TNT1 A 0 A_CustomMissile("FKFireDie",5,0,-45)
		TNT1 A 0 A_CustomMissile("FKFireDie",5,0,180)
		TNT1 A 0 A_CustomMissile("FKFireDie",5,0,135)
		TNT1 A 0 A_CustomMissile("FKFireDie",5,0,0)
		TNT1 A 0 A_CustomMissile("FKFireDie",5,0,-135)
		TNT1 A 0 A_SpawnItemEx("DustCloudCluster_NoDirt",0,0,0,0,0,0,0,0,0)
		TNT1 A 0 Radius_Quake(3,15,0,16,0)
		Goto DeathFall
	Death.KnifeSlash:
	Death.KnifeStab:
	Death.KnifeSlashPower:
	Death.KnifeStabPower:
	Death.WakizashiSlash:
	Death.WakizashiStab:
	Death.WakizashiSlashPower:
	Death.WakizashiStabPower:
		SKFA H 5
		SKFA I 5 A_PlaySound("KNI_HIT")
	DeathFall:
		SKFA J 5 A_Fall
		SKFA KLM 5
		TNT1 A 0 A_Jump(5,"Regenerating")
		SKFA N -1
		Stop
	Regenerating:
		SKFA NNNNNNNNNNNNNNN 70
		SKFA N -1 Thing_Raise(0)
		Wait
	Splode:
		TNT1 A 0 A_XScream
		TNT1 A 0 A_NoBlocking
		XXLF O 5 A_SpawnItem("AeniBlood",0,32,0)
		TNT1 A 0 A_SpawnItemEx ("AeniBlood", 0,34,0, random(-75,75)*0.01,random(-75,75)*0.01,random(-65,65)*0.01, 0, SXF_NOCHECKPOSITION|SXF_CLIENTSIDE|SXF_TRANSFERTRANSLATION, 0)	
		XXLF P 5 A_SpawnItem("AeniBlood",0,34,0)
		TNT1 A 0 A_CustomMissile("FKFireDie",5,0,90)
		TNT1 A 0 A_CustomMissile("FKFireDie",5,0,45)
		TNT1 A 0 A_CustomMissile("FKFireDie",5,0,-90)
		TNT1 A 0 A_CustomMissile("FKFireDie",5,0,-45)
		TNT1 A 0 A_CustomMissile("FKFireDie",5,0,180)
		TNT1 A 0 A_CustomMissile("FKFireDie",5,0,135)
		TNT1 A 0 A_CustomMissile("FKFireDie",5,0,0)
		TNT1 A 0 A_CustomMissile("FKFireDie",5,0,-135)
		TNT1 A 0 A_SpawnItemEx("DustCloudCluster_NoDirt",0,0,0,0,0,0,0,0,0)
		TNT1 A 0 Radius_Quake(3,15,0,16,0)
		XXLF Q 5 A_SpawnItem("AeniBlood",0,26,0)
		XXLF R 5 A_SpawnItem("AeniBlood",0,20,0)
		XXLR S 5 A_SpawnItem("AeniBlood",0,14,0)
		XXLR T 5 A_SpawnItem("AeniBlood",0,6,0)
		XXLR U 5 A_SpawnItem("AeniBlood",0,0,0)
		XXLR V 5 A_SpawnItem("AeniBlood",0,0,0)
		XXLR W 5 // No extra blood needed here. He's dead, Jim.
		TNT1 A 0 A_XScream
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_SpawnItemEx("DEADStrikeKnightGib",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
		Stop
	Death.Fire2:
		TNT1 A 0 A_CustomMissile("FKFireDie",5,0,90)
		TNT1 A 0 A_CustomMissile("FKFireDie",5,0,45)
		TNT1 A 0 A_CustomMissile("FKFireDie",5,0,-90)
		TNT1 A 0 A_CustomMissile("FKFireDie",5,0,-45)
		TNT1 A 0 A_CustomMissile("FKFireDie",5,0,180)
		TNT1 A 0 A_CustomMissile("FKFireDie",5,0,135)
		TNT1 A 0 A_CustomMissile("FKFireDie",5,0,0)
		TNT1 A 0 A_CustomMissile("FKFireDie",5,0,-135)
		TNT1 A 0 A_SpawnItemEx("DustCloudCluster_NoDirt",0,0,0,0,0,0,0,0,0)
		TNT1 A 0 Radius_Quake(3,15,0,16,0)
		TNT1 A 1 A_SpawnItem("BurnDeath1", 2, 1)
		TNT1 A 0 A_AlertMonsters
		Stop
	Death.head:
		SKFA O 9 A_PlaySound("headshot/splat")
		TNT1 A 0 A_SpawnItem("AeniBlood",0,40,0)
		TNT1 A 0 A_SpawnItem("AeniBlood",0,40,0)
		SKFA P 9 A_XScream
		SKFA Q 9 A_Fall
		SKFA R 5
		SKFA S 5 A_SpawnItemEx("DEADStrikeKnight",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
		Stop
	Raise:
		SKFA KJI 5
		SKFA H 5 Thing_SetSpecial(0,226,642,0,random(14,28))
		Goto See
	}
}

Actor FireKnightGrenade
{
  obituary "%o was scorched from a Fire Knight's fire grenade."
  radius 6
  height 8
  speed 20
  damage (10)
  seesound "ZSec/bounce"
  PROJECTILE
  +DOOMBOUNCE
  -NOGRAVITY
  +GRENADETRAIL
  +DONTFALL
  DamageType Fire
  bouncefactor 0.5
  bouncecount 10
  states
  {
  Spawn:
    GRND AAA 35 bright
  Death:
    MISL C 0 bright A_PlaySound("weapons/rocklx")
    MISL C 5 bright A_Explode(8,32,0)
    MISL C 0 A_PlaySound("Grenade/Flamed")
    TNT1 A 0 A_SpawnItemEx("DustCloudCluster_NoDirt",0,0,0,0,0,0,0,0,0)
    MISL J 0 A_CustomMissile("FKFire", 5, 0, 90)
    MISL J 0 A_CustomMissile("FKFire", 5, 0, 45)
    MISL J 0 A_CustomMissile("FKFire", 5, 0, -90)
    MISL J 0 A_CustomMissile("FKFire", 5, 0, -45)
    MISL J 0 A_CustomMissile("FKFire", 5, 0, 180)
    MISL J 0 A_CustomMissile("FKFire", 5, 0, 135)
    MISL J 0 A_CustomMissile("FKFire", 5, 0, 0)
    MISL J 0 A_CustomMissile("FKFire", 5, 0, -135)
    MISL D 4 bright
    MISL E 3 bright
    stop
  }
}


ACTOR HellFire
{
+NOBLOCKMAP
+MISSILE
+DROPOFF
+ACTIVATEIMPACT 
+ACTIVATEPCROSS 
Speed 5
Damage 0
DeathSound "dragonfam/attack"

States
{
Spawn:
NULL A 1
Loop
Death:
NULL A 0 Bright A_CustomMissile("HellFire1", 0, 0, -180)
NULL A 0 Bright A_CustomMissile("HellFire1", 0, 16, -175)
NULL A 0 Bright A_CustomMissile("HellFire1", 0, 32, -170)
NULL A 0 Bright A_CustomMissile("HellFire1", 0, -16, -185)
NULL A 0 Bright A_CustomMissile("HellFire1", 0, -32, -190)
Stop
}
}

ACTOR HellFire1
{
+NOCLIP
+MISSILE
+DROPOFF
+ACTIVATEIMPACT 
+ACTIVATEPCROSS 
Speed 2
Damage 2
States
{
Spawn:
NULL A 10 A_CustomMissile("HellFire2", 0, 0, 0)
NULL A 10 A_CustomMissile("HellFire2", 0, 16, 0)
NULL A 10 A_CustomMissile("HellFire2", 0, -16, 0)
NULL A 10 A_CustomMissile("HellFire2", 0, 0, 0)
NULL A 10 A_CustomMissile("HellFire2", 0, 16, 0)
NULL A 10 A_CustomMissile("HellFire2", 0, -16, 0)
NULL A 10 A_CustomMissile("HellFire2", 0, 0, 0)
NULL A 10 A_CustomMissile("HellFire2", 0, 16, 0)
NULL A 10 A_CustomMissile("HellFire2", 0, -16, 0)
NULL A 10 A_CustomMissile("HellFire2", 0, 0, 0)
NULL A 10 A_CustomMissile("HellFire2", 0, 16, 0)
NULL A 10 A_CustomMissile("HellFire2", 0, -16, 0)
NULL A 10 A_CustomMissile("HellFire2", 0, 0, 0)
NULL A 10 A_CustomMissile("HellFire2", 0, 16, 0)
NULL A 10 A_CustomMissile("HellFire2", 0, -16, 0)
Stop
}
}

ACTOR HellFire2
{
+NOCLIP
+MISSILE
+DROPOFF
+RANDOMIZE
+ACTIVATEIMPACT 
+ACTIVATEPCROSS 
Scale 1.5
Speed 1
Damage 2
RENDERSTYLE ADD
ALPHA 0.67
ExplosionDamage 3
ExplosionRadius 64
DeathSound "dragonfam/attack"
Damagetype "HellFire"
States
{
Spawn:
NULL A 8
FRTF A 3 Bright
FRTF A 0 A_Explode
FRTF B 3 Bright
FRTF B 0 A_Explode
FRTF C 3 Bright
FRTF C 0 A_Explode
FRTF C 0
FRTF C 0 A_Jump(191,4)
FRTF C 0 A_CustomMissile("HellFire2", 0, 24, 0)
FRTF C 0 A_CustomMissile("HellFire2", 0, 0, 0)
FRTF C 0 A_CustomMissile("HellFire2", 0, -24, 0)
FRTF DEFGHIJKLMNO 3 Bright
Stop
}
}

ACTOR FireShotCloud1 //Weapon: Poison Gun, By: Daniel, using the gas clouds from it.
{
   Radius 24
   Height 16
   Speed 16
   Mass 2000
   damage 6
   obituary "%o was lost in the inferno created by a Fire Knight."
   +NOBLOCKMAP 
   +DROPOFF 
   +MISSILE 
   +ACTIVATEIMPACT 
   +ACTIVATEPCROSS 
   +NOTARGET
   +RANDOMIZE
   RenderStyle Add
   Alpha 0.75
   scale 0.9
   SeeSound "weapon/flame"
   DeathSound "dragonfam/attack"
   DamageType Fire2
   States
   {
   Spawn:
      FRTB ABCDEFGHI 5 Bright
      FRTF A 2 Bright
      FRTF B 2 Bright
      FRTF C 2 Bright
      FRTF C 0 A_Jump(16,2)
      FRTF C 2 Bright A_CustomMissile("FireShotCloud2",5,0,0)
      FRTF C 0 A_Jump(16,2)
      FRTF C 2 Bright A_CustomMissile("FireShotCloud2",5,0,90)
      FRTF C 0 A_Jump(16,2)
      FRTF C 2 Bright A_CustomMissile("FireShotCloud2",5,0,180)
      FRTF C 0 A_Jump(16,2)
      FRTF C 2 Bright A_CustomMissile("FireShotCloud2",5,0,270)
      FRTF DEFGHIJKLM 2 Bright
      Loop
   Death:
      FRTF NO 2 Bright
      Stop
   }
}

ACTOR FireShotCloud2
{
   Radius 16
   Height 8
   Speed 8
   Mass 3000
   damage 6
   obituary "%o was lost in the inferno created by a Fire Knight."   
   +NOBLOCKMAP 
   +DROPOFF 
   +MISSILE 
   +ACTIVATEIMPACT 
   +ACTIVATEPCROSS 
   +NOTARGET
   +RANDOMIZE
   RenderStyle Add
   Alpha 0.75
   scale 0.9
   SeeSound "weapon/flame"
   DeathSound "dragonfam/attack"
   DamageType Fire2
   States
   {
   Spawn:
      FRTF A 1 Bright
      FRTF A 2 Bright
      FRTF B 2 Bright
      FRTF C 2 Bright
      FRTF C 0 A_Jump(32,2)
      FRTF C 2 Bright A_CustomMissile("FireShotCloud3",5,0,0)
      FRTF C 0 A_Jump(32,2)
      FRTF C 2 Bright A_CustomMissile("FireShotCloud3",5,0,90)
      FRTF C 0 A_Jump(32,2)
      FRTF C 2 Bright A_CustomMissile("FireShotCloud3",5,0,180)
      FRTF C 0 A_Jump(32,2)
      FRTF C 2 Bright A_CustomMissile("FireShotCloud3",5,0,270)
      FRTF DEFGHIJKLM 2 Bright
      Loop
   Death:
      FRTF NO 2 Bright
      Stop
   }
}

ACTOR FireShotCloud3
{
   Radius 10
   Height 8
   Speed 8
   Mass 4000
   damage 6
   obituary "%o was lost in the inferno created by a Fire Knight."
   +NOBLOCKMAP 
   +DROPOFF 
   +MISSILE 
   +ACTIVATEIMPACT 
   +ACTIVATEPCROSS 
   +NOTARGET
   +RANDOMIZE
   RenderStyle Add
   Alpha 0.75
   scale 0.9
   SeeSound "weapon/flame"
   DeathSound "dragonfam/attack"
   DamageType Fire2
   States
   {
   Spawn:
      FRTF A 1 Bright
      FRTF A 2 Bright
      FRTF B 2 Bright
      FRTF CDEFGHIJKLM 2 Bright
      Loop
   Death:
      FRTF NO 2 Bright
      Stop
   }
}

actor FireBlast
{
  Game Doom
  SpawnID 51
  Radius 13
  Height 8
  Speed 25
  Damage 6
  Projectile
  +RANDOMIZE
  RenderStyle Add
  Alpha 0.75
  scale 0.9
  SeeSound "weapon/flame"
  DeathSound "dragonfam/attack"
  DamageType Fire2
  Decal Scorch
  States
  {
  Spawn:
    FRTM AB 6 bright
    loop
  Death:
    FRTM CDE 4 bright A_Explode(64,128,0)
    stop
  }
}

ACTOR FKFire
{
   Radius 2
   Height 4
   Speed 20
   Damage 10
   PROJECTILE
   RENDERSTYLE ADD
   DamageType Fire2
   ALPHA 0.67
   Scale 0.67
   SeeSound "weapons/flamer"
   DeathSound "weapons/scorch"
   DONTHURTSHOOTER
   +THRUGHOST
   States
   {
   Spawn:
      NULL A 2 Bright
      FRFX ABCD 2 Bright A_Explode(2,8,0)
      FRFX D 0 Bright A_Gravity
      FRFX EFG 2 Bright A_Explode(2,16,0)
      FRFX HIJ 2 Bright A_Explode(2,32,0)
      FRFX KLM 2 Bright A_Explode(2,64,0)
      FRFX NO 2 Bright
      //stop
   Death:
      FRFX HIJ 2 Bright A_Explode(4,32)
	  TNT1 A 0 A_SpawnItemEx("IgnitedSurface",0,0,1,random(-16,16),random(-16,16),random(0,2))//random(-15,15),random(-15,15),random(5,15),random(-8,8),random(-8,8),random(0,2))
      //TNT1 A 0 A_CustomMissile("IgnitedSurface", random(5,15), random(-15, 15), 0, 0)
      //FRFX J 0 A_CustomMissile ("FKDropFire",0,0,0)
      FRFX KLM 2 Bright A_Explode(4,64,0)
      FRFX NO 2 Bright
      stop
   }
}

ACTOR FKDropFire
{
   Radius 8
   Height 40
   PROJECTILE
   RENDERSTYLE ADD
   DamageType Fire2
   ALPHA 0.90
   DONTHURTSHOOTER
   -NOGRAVITY
   +NOEXPLODEFLOOR
   +NODAMAGETHRUST
   +THRUGHOST
   Gravity 0.25
   States
   {
   Spawn:
      FLME A 1 Bright A_Playsound("weapons/onfire")
      FLME A 1 Bright A_Explode(2,16,0)
      FLME BCDEFGHIJKLMN 2 Bright A_Explode(4,32,0)
      FLME A 1 Bright A_Playsound("weapons/onfire")
      FLME A 1 Bright A_Explode(2,32,0)
      FLME BCDEFGHIJKLMN 2 Bright A_Explode(4,32,0)
      FLME A 1 Bright A_Playsound("weapons/onfire")
      FLME A 1 Bright A_Explode(2,32,0)
      FLME BCDEFGHIJKLMN 2 Bright A_Explode(4,32,0)
      FLME A 1 Bright A_Playsound("weapons/onfire")
      FLME A 1 Bright A_Explode(2,32,0)
      FLME BCDEFGHIJKLMN 2 Bright A_Explode(4,32,0)
      FLME A 1 Bright A_Playsound("weapons/onfire")
      FLME A 1 Bright A_Explode(2,32,0)
      FLME BCDEFGHIJKLMN 2 Bright A_Explode(4,32,0)
      FLME A 0 A_Jump(192,1)
      loop
      NULL A 0
      goto death
   Death:
      NULL A 1 Bright
      stop
   }
}

ACTOR FKFireDie
{
   Radius 2
   Height 4
   Speed 4
   Damage 10
   PROJECTILE
   RENDERSTYLE ADD
   DamageType Fire2
   ALPHA 0.67
   Scale 0.67
   SeeSound "weapons/flamer"
   DeathSound "weapons/scorch"
   DONTHURTSHOOTER
   +THRUGHOST
   States
   {
   Spawn:
      NULL A 2 Bright
      FRFX ABCD 2 Bright A_Explode(4,8,0)
      FRFX D 0 Bright A_Lowgravity
      FRFX EFG 2 Bright A_Explode(4,16,0)
      FRFX HIJ 2 Bright A_Explode(4,32,0)
      FRFX KLM 2 Bright A_Explode(4,64,0)
      FRFX NO 2 Bright
      stop
   Death:
      FRFX HIJ 2 Bright A_Explode(4,32,0)
      TNT1 A 0 A_CustomMissile("IgnitedSurface", random(1,5), random(-1, 1), 0, 0)
      //FRFX J 0 A_CustomMissile ("FKDropFire",0,0,0,4)
      FRFX KLM 2 Bright A_Explode(4,64,0)
      FRFX NO 2 Bright
      stop
   }
}

ACTOR FKFireGas : FKFire {
	Speed 4
	+RIPPER
	ReactionTime 8
	States {
		Spawn:
			TNT1 A 0
			FRFX ABCD 2 Bright A_Explode(2,8,0)
			FRFX EFG 2 Bright A_Explode(2,16,0)
			FRFX HIJ 2 Bright A_Explode(2,32,0)
			FRFX KLMNO 2 Bright A_Explode(2,64,0)
			//FRFX NO 2 Bright 
			TNT1 A 0 A_CountDown
			Loop
		Death:
			FRFX HIJ 2 Bright A_Explode(4,32)
			TNT1 A 0 A_SpawnItemEx("IgnitedSurface",0,0,1,random(-16,16),random(-16,16),random(0,2))
			FRFX KLM 2 Bright A_Explode(4,64,0)
			FRFX NO 2 Bright
			stop
	}
}