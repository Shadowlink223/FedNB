ACTOR StrikeKnight : Enemy 13202 {
	//$Category Federation

	Health 225
	GibHealth 60
	Radius 16
	Height 40
	Mass 100
	Speed 14

	+MISSILEMORE
	//+FASTER

	DropItem "Clip" 128
	DropItem "Clip" 64
	DropItem "RocketAmmo" 16

	DropItem "Stimpack" 32
	DropItem "StrikeArmor" 4
	DropItem "ArmorShardSpawner" 64

	DropItem "Chaingun" 4

	DropItem "StimKit" 4

	//Dropitem "PDA" 4

	Dropitem "7Beacon" 2

	Translation 2

	BloodType "AeniBlood"
	Decal "BulletChip"

	PainChance 50

	SeeSound "Knight/KnightSight"
	AttackSound ""
	PainSound "Knight/KnightPain"
	DeathSound "monster/inidth"
	ActiveSound "monster/iniact"

	Obituary "%o felt the force of a Strike Knight." 
	HitObituary "A Strike Knight quick slashed %o with their knife." 

	States {

/******************************************************************************\
==================================MAIN STATES===================================
\******************************************************************************/

	Spawn:
		SKFA A 0
		TNT1 A 0 Thing_SetSpecial(0,226,642,0,random(14,28))
	SpawnLoop:
		TNT1 A 0 A_SpawnItem("HeadshotTarget10",0,40,0)
		SKFA E 10 A_LookEx(0,0,random(512,2048),random(1024,4096),random(120,120),"See")
		Loop
	See:
		SKFA A 1 // Infinite loop crash prevention?
		TNT1 A 0 A_CheckSight("ChanceToCharge")
		Goto TakeCharge
	ChanceToCharge:
		TNT1 A 0 A_Jump(128,"GiveCharge")
		TNT1 A 0 A_TakeInventory("DealWithIt",1)
		Goto Chase
	GiveCharge:
		TNT1 A 0 A_JumpIfInventory("KnightCharge",1,"TakeCharge")
		TNT1 A 0 A_GiveInventory("KnightCharge",1)
		Goto IfLowHealth
	TakeCharge:
		TNT1 A 0 A_TakeInventory("KnightCharge",1)
		Goto IfLowHealth
	IfLowHealth:
		TNT1 A 0 A_JumpIfHealthLower(50,"LowHealth")
		TNT1 A 0 A_TakeInventory("DealWithIt",1)
		Goto HealthHigh
	HealthHigh:
		TNT1 A 0 A_ChangeFlag("Frightened",0)
		Goto Chase
	LowHealth:
		TNT1 A 0 A_Jump(32,"HealUp")
		TNT1 A 0 A_TakeInventory("DealWithIt",1)
		Goto Retreat
	Retreat:
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_ChangeFlag("Frightened",1)
		Goto HealUp
	HealUp:
		TNT1 A 0 HealThing(1)
		Goto Chase
	Chase:
		TNT1 A 0 A_JumpIfInventory("KnightCharge",1,"Charge")
		TNT1 A 0 A_SpawnItem("HeadshotTarget3",0,40,0)
		SKFA A 3 A_Chase("missile","missile",CHF_FASTCHASE)
		TNT1 A 0 A_SpawnItem("HeadshotTarget3",0,40,0)
		SKFA B 3 A_Chase
		TNT1 A 0 A_SpawnItem("HeadshotTarget3",0,40,0)
		SKFA C 3 A_Chase
		TNT1 A 0 A_SpawnItem("HeadshotTarget3",0,40,0)
		SKFA D 3 A_Chase
		Goto See
	Charge:
		TNT1 A 0 A_SpawnItem("HeadshotTarget3",0,40,0)
		SKFA A 2 A_Chase
		TNT1 A 0 A_SpawnItem("HeadshotTarget3",0,40,0)
		SKFA B 3 A_Chase
		TNT1 A 0 A_SpawnItem("HeadshotTarget3",0,40,0)
		SKFA C 3 A_Chase
		TNT1 A 0 A_SpawnItem("HeadshotTarget3",0,40,0)
		SKFA D 3 A_Chase
		Goto See

/******************************************************************************\
=================================ATTACK STATES==================================
\******************************************************************************/

	Missile:
		TNT1 A 0 A_SpawnItem("HeadshotTarget10",0,40,0)
		SKFA E 10 A_FaceTarget
		TNT1 A 0 A_Jump(64,"DistanceCheck")
		Goto BulletAttack
	BulletAttack:
		TNT1 A 0 A_JumpIfInventory("KnightAmmo",40,"Reload")
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("MP7_FIRE",6)
		TNT1 A 0 A_GiveInventory("KnightAmmo",1)
		TNT1 A 0 A_SpawnItem("HeadshotTarget1",0,40,0)
		//TNT1 A 0 A_CustomBulletAttack(5,5,1,random(1,3),BulletPuff)
		SKFA F 1 BRIGHT A_CustomMissile("StrikeTracer",32,0,random(-5,5),0,random(-5,5))
		TNT1 A 0 BRIGHT A_SpawnItemEx("DukeCasing1",random(-4,4),5,32,0,10,Random(0,3),0,32)
		SKFA E 1 A_SpawnItem("HeadshotTarget1",0,40,0)
		TNT1 A 0 A_JumpIfInventory("KnightAmmo",40,"Reload")
		TNT1 A 0 A_MonsterRefire(224,"See")
		Loop
	Reload:
		TNT1 A 0 A_ChangeFlag(NOPAIN,1)
		TNT1 A 0 A_SpawnItem("HeadshotTarget2",0,40,0)
		SKFA E 2 A_TakeInventory("KnightAmmo",40)
		SKFA EE 10 A_SpawnItem("HeadshotTarget10",0,40,0)
		TNT1 A 0 A_PlaySound("MP7/MagOut",5)
		SKFA E 2 A_SpawnItem("HeadshotTarget2",0,40,0)
		SKFA EEEE 10 A_SpawnItem("HeadshotTarget10",0,40,0)
		TNT1 A 0 A_PlaySound("MP7/MagIn1",5)
		SKFA E 2 A_SpawnItem("HeadshotTarget2",0,40,0)
		SKFA E 10 A_SpawnItem("HeadshotTarget10",0,40,0)
		TNT1 A 0 A_PlaySound("MP7/MagIn2",5)
		SKFA E 2 A_SpawnItem("HeadshotTarget2",0,40,0)
		SKFA E 10 A_SpawnItem("HeadshotTarget10",0,40,0)
		TNT1 A 0 A_PlaySound("MP7/BoltRelease",5)
		SKFA E 2 A_SpawnItem("HeadshotTarget2",0,40,0)
		SKFA EEE 10 A_SpawnItem("HeadshotTarget10",0,40,0)
		TNT1 A 0 A_PlaySound("MP7/Handle",5)
		SKFA EE 3 A_SpawnItem("HeadshotTarget3",0,40,0)
		SKFA EE 10 A_SpawnItem("HeadshotTarget10",0,40,0)
		TNT1 A 0 A_ChangeFlag(NOPAIN,0)
		goto See
	DistanceCheck:
		TNT1 A 0 A_JumpIfCloser(1768,"CloseRangeSpecial")
		TNT1 A 0 A_TakeInventory("DealWithIt",1)
		Goto BulletAttack
	CloseRangeSpecial:
		TNT1 A 0 A_SpawnItem("HeadshotTarget4",0,40,0)
		SKFA E 4 A_FaceTarget
		TNT1 A 0 A_JumpIfCloser(192,"KnifeLunge")
		Goto ThrowGrenade
	ThrowGrenade:
		TNT1 A 0 A_PlaySound("GRE_THRO",CHAN_AUTO)
		TNT1 A 0 A_SpawnItem("HeadshotTarget2",0,40,0)
		SKFA E 2 A_CustomMissile("ThrownGrenade",28,5,random(-15,15),0)	//("KnightGrenadeShell",28,5,random(-15,15),2,random(0,10))
		Goto CloseRangeFinish
	KnifeLunge:
		TNT1 A 0 ThrustThing(angle*256/360,25,0,0)
		TNT1 A 0 A_SpawnItem("HeadshotTarget4",0,40,0)
		SKFA E 4 A_FaceTarget
		TNT1 A 0 A_PlaySound("knife/swing")
		TNT1 A 0 A_SpawnItem("HeadshotTarget2",0,40,0)
		SKFA E 2 A_CustomMeleeAttack(34,"knife/swing","knife/swing","Knife",1)
		Goto CloseRangeFinish
	CloseRangeFinish:
		SKFA EE 3 A_SpawnItem("HeadshotTarget3",0,40,0)
		Goto See

/******************************************************************************\
==================================PAIN STATES===================================
\******************************************************************************/

	Pain:
		SKFA G 3 A_SpawnItem("HeadshotTarget4",0,40,0)
		TNT1 A 0 A_SpawnItem("HeadshotTarget4",0,40,0)
		SKFA G 3 A_Pain
		Goto See
	Pain.Shock:
	Pain.ShockSlug:
		TNT1 A 0 A_ChangeFlag(NOPAIN,1)
		TNT1 A 0 A_Pain
		SKFA GGGGGGGGGGGGGGGGGGGGGGGGGGGGGG 10 A_SpawnItem("HeadshotTarget10",0,40,0)
		TNT1 A 0 A_ChangeFlag(NOPAIN,0)
		Goto See
	Pain.KillMe:
		Goto See

/******************************************************************************\
==================================DEATH STATES==================================
\******************************************************************************/

	Death:
		TNT1 A 0 A_JumpIfHealthLower(-60,"Splode")
		SKFA H 5 A_AlertMonsters
		SKFA I 5 A_Scream
		Goto DeathFall
	Death.KnifeSlash:
	Death.KnifeStab:
	Death.KnifeSlashPower:
	Death.KnifeStabPower:
	Death.WakizashiSlash:
	Death.WakizashiStab:
	Death.WakizashiSlashPower:
	Death.WakizashiStabPower:
		SKFA H 5
		SKFA I 5 A_PlaySound("KNI_HIT")
	DeathFall:
		SKFA J 5 A_Fall
		SKFA KLM 5
		TNT1 A 0 A_Jump(5,"Regenerating")
		SKFA N -1
		Stop
	Regenerating:
		SKFA NNNNNNNNNNNNNNN 70
		SKFA N -1 Thing_Raise(0)
		Wait
	Splode:
		TNT1 A 0 A_XScream
		TNT1 A 0 A_NoBlocking
		XXLF O 5 A_SpawnItem("AeniBlood",0,32,0)
		TNT1 A 0 A_SpawnItemEx ("AeniBlood", 0,34,0, random(-75,75)*0.01,random(-75,75)*0.01,random(-65,65)*0.01, 0, SXF_NOCHECKPOSITION|SXF_CLIENTSIDE|SXF_TRANSFERTRANSLATION, 0)	
		XXLF P 5 A_SpawnItem("AeniBlood",0,34,0)
		XXLF Q 5 A_SpawnItem("AeniBlood",0,26,0)
		XXLF R 5 A_SpawnItem("AeniBlood",0,20,0)
		XXLR S 5 A_SpawnItem("AeniBlood",0,14,0)
		XXLR T 5 A_SpawnItem("AeniBlood",0,6,0)
		XXLR U 5 A_SpawnItem("AeniBlood",0,0,0)
		XXLR V 5 A_SpawnItem("AeniBlood",0,0,0)
		XXLR W 5 // No extra blood needed here. He's dead, Jim.
		TNT1 A 0 A_SpawnItemEx("DEADStrikeKnightGib",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
		Stop
	Death.Fire2:
		TNT1 A 1 A_SpawnItem("BurnDeath1", 2, 1)
		TNT1 A 0 A_AlertMonsters
		Stop
	Death.head:
		SKFA O 9 A_PlaySound("headshot/splat")
		//TNT1 AAA 0 A_SpawnItemEx("HelmetChunkGibber",0,0,0,random(-5,5),random(-5,5),random(-5,15),0,SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
		TNT1 A 0 A_SpawnItem("AeniBlood",0,40,0)
		TNT1 A 0 A_SpawnItem("AeniBlood",0,40,0)
		SKFA P 9 A_XScream
		SKFA Q 9 A_Fall
		SKFA R 5
		SKFA S 5 A_SpawnItemEx("DEADStrikeKnight",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
		Stop
	Raise:
		SKFA KJI 5
		SKFA H 5 Thing_SetSpecial(0,226,642,0,random(14,28))
		Goto See
	}
}

ACTOR DEADStrikeKnight {
	+FLOORCLIP
	Radius 12
	Height 11
	States {
		Spawn:
			SKFA T 0
			SKFA T -1
			stop
	}
}

ACTOR DEADStrikeKnightHead {
	+FLOORCLIP
	Radius 12
	Height 11
	States {
		Spawn:
			SKFA Z 0
			SKFA Z -1
			stop
	}
}

ACTOR DEADStrikeKnightGib {
	+FLOORCLIP
	Radius 12
	Height 11
	States {
		Spawn:
			XXLR W 0
			XXLR W -1
			stop
	}
}

ACTOR ThrownGrenade {
	Radius 2
	Height 4
	Speed 20 // previously 24

	Damage 1
	damagetype "Explosion"

	Scale 0.15

	gravity 0.4
	bouncefactor 0.5
	WallBounceFactor 0.5
	ReactionTime 4

	PROJECTILE
	-NOGRAVITY
	+HEXENBOUNCE
	//+LOWGRAVITY
	+NOEXTREMEDEATH
	//+RANDOMIZE
	+FORCERADIUSDMG
	+CANBOUNCEWATER
	+BOUNCEONACTORS
	//+GRENADETRAIL
	//+FULLVOLDEATH

	//SeeSound "weapons/grnbounce"
	DeathSound "Grenade/Explode"

	States {
		Spawn:
			GHEA A 0
			TNT1 A 0 ACS_ExecuteAlways(557,0)
		SpawnLoop:
			TNT1 A 0 A_JumpIf(momx < 1.0 && momy < 1.0 && momz < 1.0,"StopAnimation")
			GHEA AEBDBEA 4
			GHEA C 4 A_Countdown
			Loop
			
		StopAnimation:
			GHEA EEEMEEE 4
			GHEA M 4 A_Countdown
			Loop

		Death:
			GHEA A 0 A_AlertMonsters
			GHEA A 0 //A_PlaySound("weapons/grnexplode")
			TNT1 A 0 A_SpawnItemEx("DustCloudCluster",0,0,0,0,0,0,0,0,0)
			GHEA A 0 A_SpawnItemEx("Explosion",0,0,1,0,0,0,0,0,0) //Changed from "GrenadeExplosion". If it's broke revert back! also now 1 instead of -1
			GHEA A 3 Bright
			Stop
	}
}

ACTOR RolledGrenade : ThrownGrenade {
	Speed 21 // previously 25
	States {
		Spawn:
			GHEA A 0
			TNT1 A 0 ACS_ExecuteAlways(557,0)
		SpawnLoop:
			TNT1 A 0 A_JumpIf(momx < 1.0 && momy < 1.0 && momz < 1.0,"StopAnimation")
			GHEA EFGHIJK 4
			GHEA L 4 A_Countdown
			Loop
	}
}

ACTOR ThrownPlasmaGrenade {
	Radius 2
	Height 4
	Speed 20 // previously 24

	Damage 1
	damagetype "Explosion"

	Scale 0.15

	gravity 0.4
	bouncefactor 0.5
	WallBounceFactor 0.5
	ReactionTime 4

	PROJECTILE
	-NOGRAVITY
	+HEXENBOUNCE
	//+LOWGRAVITY
	+NOEXTREMEDEATH
	//+RANDOMIZE
	+FORCERADIUSDMG
	+CANBOUNCEWATER
	+BOUNCEONACTORS
	//+GRENADETRAIL
	//+FULLVOLDEATH

	//SeeSound "weapons/grnbounce"
	DeathSound "Grenade/Explode"

	States {
		Spawn:
			GHEA A 0
			TNT1 A 0 ACS_ExecuteAlways(557,0)
		SpawnLoop:
			TNT1 A 0 A_JumpIf(momx < 1.0 && momy < 1.0 && momz < 1.0,"StopAnimation")
			GHEA AEBDBEA 4
			GHEA C 4 A_Countdown
			Loop
			
		StopAnimation:
			GHEA EEEMEEE 4
			GHEA M 4 A_Countdown
			Loop

		Death:
			GHEA A 0 A_AlertMonsters
			GHEA A 0 //A_PlaySound("weapons/grnexplode")
			TNT1 A 0 A_SpawnItemEx("DustCloudCluster",0,0,0,0,0,0,0,0,0)
			GHEA A 0 A_SpawnItemEx("PlasmaExplosion",0,0,1,0,0,0,0,0,0) //Changed from "GrenadeExplosion". If it's broke revert back! also now 1 instead of -1
			GHEA A 3 Bright
			Stop
	}
}

ACTOR RolledPlasmaGrenade : ThrownPlasmaGrenade {
	Speed 21 // previously 25
	States {
		Spawn:
			GHEA A 0
			TNT1 A 0 ACS_ExecuteAlways(557,0)
		SpawnLoop:
			TNT1 A 0 A_JumpIf(momx < 1.0 && momy < 1.0 && momz < 1.0,"StopAnimation")
			GHEA EFGHIJK 4
			GHEA L 4 A_Countdown
			Loop
	}
}