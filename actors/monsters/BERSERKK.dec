ACTOR BerserkKnight : Enemy 13774 {
  //$Category Federation
  
  Health 135
  GibHealth 60
  Radius 16
  Height 40
  Mass 750
  Speed 10 //30
  
  +MISSILEMORE
  +FASTER
  
  DropItem "Clip" 128
  DropItem "Clip" 64
  DropItem "RocketAmmo" 16
  DropItem "RocketAmmo" 8
  
  Dropitem "Stimpack" 32
  Dropitem "StrikeArmor" 4
  DropItem "ArmorShardSpawner" 64
  
  DropItem "Chaingun" 4
  
  Translation "112:127=176:191"
  
  BloodType "AeniBlood"
  Decal BulletChip
  
	DamageFactor "DU", 1.1
	DamageFactor "HP", 1.25
	DamageFactor "Trid", 1.5
  
  PainChance 1
  PainChance "Shock", 64
  PainChance "ShockSlug", 64
  
  MeleeThreshold 256
  MeleeDamage 10
  
  SeeSound "Phage/Sight"
  AttackSound "MP7_FIRE"
  PainSound "Phage/Pain"
  DeathSound "Phage/Death"
  ActiveSound "Phage/Active"
  
  damagetype "Berserker"
  damagefactor "Berserker", 0
  
  Obituary "A Berserk Knight took out his rage on %o." 
  Hitobituary "%o was piledrived by a Berserk Knight!"
  
  States {
  
/******************************************************************************\
================================HEADSHOT STATES=================================
\******************************************************************************/

	Spawn:
		GO99 A 0
		TNT1 A 0 Thing_SetSpecial(0,226,642,0,random(8,10))
	SpawnLoop:
		TNT1 A 0 A_SpawnItem("HeadshotTarget3",15,40,0)
		GO99 A 3 A_LookEx(0,0,random(512,2048),random(1024,4096),random(90,120),"See")
		TNT1 A 0 A_SpawnItem("HeadshotTarget3",15,40,0)
		GO99 B 3 A_LookEx(0,0,random(512,2048),random(1024,4096),random(90,120),"See")
		TNT1 A 0 A_SpawnItem("HeadshotTarget2",15,40,0)
		GO99 A 2 A_LookEx(0,0,random(512,2048),random(1024,4096),random(90,120),"See")
		TNT1 A 0 A_SpawnItem("HeadshotTarget2",15,40,0)
		GO99 B 2 A_LookEx(0,0,random(512,2048),random(1024,4096),random(90,120),"See")
		TNT1 A 0 A_SpawnItem("HeadshotTarget1",15,40,0)
		GO99 A 1 A_LookEx(0,0,random(512,2048),random(1024,4096),random(90,120),"See")
		TNT1 A 0 A_SpawnItem("HeadshotTarget1",15,40,0)
		GO99 B 1 A_LookEx(0,0,random(512,2048),random(1024,4096),random(90,120),"See")
		Loop
	See:
		GO99 A 1 // Infinite loop crash prevention?
		TNT1 A 0 A_CheckSight("ChanceToCharge")
		Goto TakeCharge
	ChanceToCharge:
		TNT1 A 0 A_Jump(128,"GiveCharge")
		TNT1 A 0 A_TakeInventory("DealWithIt",1)
		Goto Chase
	GiveCharge:
		TNT1 A 0 A_JumpIfInventory("KnightCharge",1,"TakeCharge")
		TNT1 A 0 A_GiveInventory("KnightCharge",1)
		Goto IfLowHealth
	TakeCharge:
		TNT1 A 0 A_TakeInventory("KnightCharge",1)
		Goto IfLowHealth
	IfLowHealth:
		TNT1 A 0 A_JumpIfHealthLower(50,"LowHealth")
		TNT1 A 0 A_TakeInventory("DealWithIt",1)
		Goto HealthHigh
	HealthHigh:
		TNT1 A 0 A_ChangeFlag("Frightened",0)
		Goto Chase
	LowHealth:
		TNT1 A 0 A_Jump(32,"HealUp")
		TNT1 A 0 A_TakeInventory("DealWithIt",1)
		Goto Retreat
	Retreat:
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_ChangeFlag("Frightened",1)
		Goto HealUp
	HealUp:
		TNT1 A 0 HealThing(1)
		Goto Chase
	Chase:
		TNT1 A 0 A_JumpIfInventory("KnightCharge",1,"Charge")
		TNT1 A 0 A_SpawnItem("HeadshotTarget2",15,40,0)
		GO99 AA 2 A_FastChase
		TNT1 A 0 A_SpawnItem("HeadshotTarget2",15,40,0)
		GO99 BB 2 A_Chase
		TNT1 A 0 A_SpawnItem("HeadshotTarget2",15,40,0)
		GO99 CC 2 A_Chase
		TNT1 A 0 A_SpawnItem("HeadshotTarget2",15,40,0)
		GO99 DD 2 A_Chase
		Goto See
	Charge:
		TNT1 A 0 A_SpawnItem("HeadshotTarget2",15,40,0)
		TNT1 A 0 A_Chase
		GO99 A 2 A_Chase
		TNT1 A 0 A_SpawnItem("HeadshotTarget2",15,40,0)
		TNT1 A 0 A_Chase
		GO99 B 2 A_Chase
		TNT1 A 0 A_SpawnItem("HeadshotTarget2",15,40,0)
		TNT1 A 0 A_Chase
		GO99 C 2 A_Chase
		GO99 A 0 A_SpawnItem("HeadshotTarget2",15,40,0)
		TNT1 A 0 A_Chase
		GO99 D 2 A_Chase
		Goto See
	
/******************************************************************************\
=================================ATTACK STATES==================================
\******************************************************************************/

	Missile:
		TNT1 A 0 A_SpawnItem("HeadshotTarget10",0,40,0)
		GO99 E 10 A_FaceTarget
		TNT1 A 0 A_Jump(192,"IfClose")
		Goto BulletAttack
	BulletAttack:
		TNT1 A 0 A_JumpIfInventory("KnightAmmo",40,"Reload")
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("MP7_FIRE",6)
		TNT1 A 0 A_GiveInventory("KnightAmmo",1)
		TNT1 A 0 A_SpawnItem("HeadshotTarget1",0,40,0)
		//TNT1 A 0 A_CustomBulletAttack(5,5,1,random(1,3),BulletPuff)
		GO99 F 1 BRIGHT A_CustomMissile("StrikeTracer",38,0,random(-5,5),0,random(-5,5))
		GO99 E 1 A_SpawnItem("HeadshotTarget1",0,40,0)
		TNT1 A 0 A_JumpIfInventory("KnightAmmo",40,"Reload")
		TNT1 A 0 A_MonsterRefire(224,"See")
		Loop
	Reload:
		TNT1 A 0 A_ChangeFlag(NOPAIN,1)
		TNT1 A 0 A_SpawnItem("HeadshotTarget2",0,40,0)
		GO99 E 2 A_TakeInventory("KnightAmmo",40)
		GO99 EE 10 A_SpawnItem("HeadshotTarget10",0,40,0)
		TNT1 A 0 A_PlaySound("MP7/MagOut",5)
		GO99 E 2 A_SpawnItem("HeadshotTarget2",0,40,0)
		GO99 EEEE 10 A_SpawnItem("HeadshotTarget10",0,40,0)
		TNT1 A 0 A_PlaySound("MP7/MagIn1",5)
		GO99 E 2 A_SpawnItem("HeadshotTarget2",0,40,0)
		GO99 E 10 A_SpawnItem("HeadshotTarget10",0,40,0)
		TNT1 A 0 A_PlaySound("MP7/MagIn2",5)
		GO99 E 2 A_SpawnItem("HeadshotTarget2",0,40,0)
		GO99 E 10 A_SpawnItem("HeadshotTarget10",0,40,0)
		TNT1 A 0 A_PlaySound("MP7/BoltRelease",5)
		GO99 E 2 A_SpawnItem("HeadshotTarget2",0,40,0)
		GO99 EEE 10 A_SpawnItem("HeadshotTarget10",0,40,0)
		TNT1 A 0 A_PlaySound("MP7/Handle",5)
		GO99 EE 3 A_SpawnItem("HeadshotTarget3",0,40,0)
		GO99 EE 10 A_SpawnItem("HeadshotTarget10",0,40,0)
		TNT1 A 0 A_ChangeFlag(NOPAIN,0)
		goto See
	IfClose:
		TNT1 A 0 A_SpawnItem("HeadshotTarget4",0,40,0)
		TNT1 A 0 A_JumpIfCloser(1768,"CloseRangeSpecial")
		TNT1 A 0 A_TakeInventory("DealWithIt",1)
		Goto BulletAttack
	CloseRangeSpecial:
		GO99 E 4 A_FaceTarget
		TNT1 A 0 A_Jump(192,"Leap")
		TNT1 A 0 A_JumpIfCloser(192,"KnifeLunge")
		Goto ThrowGrenade
	ThrowGrenade:
		TNT1 A 0 A_PlaySound("GRE_THRO",CHAN_AUTO)
		TNT1 A 0 A_SpawnItem("HeadshotTarget2",0,40,0)
		GO99 E 2 A_CustomMissile("ThrownGrenade",28,5,random(-15,15),0)
		Goto CloseRangeFinish
	KnifeLunge:
		TNT1 A 0 ThrustThing(angle*256/360,25,0,0)
		TNT1 A 0 A_SpawnItem("HeadshotTarget4",0,40,0)
		GO99 E 4 A_FaceTarget
		TNT1 A 0 A_PlaySound("knife/swing")
		TNT1 A 0 A_SpawnItem("HeadshotTarget2",0,40,0)
		GO99 E 2 A_CustomMeleeAttack(34,"knife/swing","knife/swing","Knife",1)
		Goto CloseRangeFinish
	CloseRangeFinish:
		GO99 EE 3 A_SpawnItem("HeadshotTarget3",0,40,0)
		Goto See
	Leap:
		TNT1 A 0 A_ChangeFlag("NoPain",1)
		//TNT1 A 0 A_ChangeFlag("NoDamage",1)
		TNT1 A 0 A_SpawnItem("HeadshotTarget2",0,40,0)
		GO99 A 2 A_FaceTarget
		TNT1 A 0 A_SpawnItem("HeadshotTarget10",0,40,0)
		GO99 A 10 A_FaceTarget
		TNT1 A 0 A_PlaySound("Phage/Leap")
		GO99 B 1 ThrustThingZ(0,28,0,0)
		TNT1 A 0 ThrustThing(angle*256/360,20,0,0)
		TNT1 A 0 A_TakeInventory("LeapTimer",140)
		Goto MidLeap
	MidLeap:
		TNT1 A 0 A_SpawnItem("HeadshotTarget1x",0,40,0)
		GO99 B 1 //A_Explode(25,44,0,1)
		TNT1 A 0 A_CheckFloor("Land")
		TNT1 A 0 A_JumpIf(waterlevel == 1,"Land")
		TNT1 A 0 A_JumpIf(MomZ == 0,"Land")
		//TNT1 A 0 A_JumpIfInventory("LeapTimer",140,"Land")
		//TNT1 A 0 A_GiveInventory("LeapTimer",1)
		Loop
	Land:
		TNT1 A 0 A_ChangeFlag("NoPain",0)
		//TNT1 A 0 A_ChangeFlag("NoDamage",0)
		TNT1 A 0 A_SpawnItem("HeadshotTarget1x",0,40,0)
		GO99 A 1 A_Stop
		GO99 A 2 A_SpawnItem("HeadshotTarget2",0,40,0)
		Goto See
	
/******************************************************************************\
==================================PAIN STATES===================================
\******************************************************************************/

	Pain: 
		GO99 G 3 A_SpawnItem("HeadshotTarget3",0,40,0)
		TNT1 A 0 A_SpawnItem("HeadshotTarget3",0,40,0)
		GO99 G 3 A_Pain
		Goto See
	Pain.Shock:
	Pain.ShockSlug:
		TNT1 A 0 A_ChangeFlag(NOPAIN,1)
		TNT1 A 0 A_Pain
		GO99 GGGGGGGGGGGGGGGGGGGGGGGGGGGGGG 10 A_SpawnItem("HeadshotTarget10",0,40,0)
		TNT1 A 0 A_ChangeFlag(NOPAIN,0)
		Goto See
	Pain.KillMe:
		Goto See
	
/******************************************************************************\
==================================DEATH STATES==================================
\******************************************************************************/

	Death: 
		TNT1 A 0 A_JumpIfHealthLower(-60,"Splode")
		GO99 H 5 A_AlertMonsters
		GO99 I 5 A_Scream
		Goto DeathFall
	Death.KnifeSlash:
	Death.KnifeStab:
	Death.KnifeSlashPower:
	Death.KnifeStabPower:
	Death.WakizashiSlash:
	Death.WakizashiStab:
	Death.WakizashiSlashPower:
	Death.WakizashiStabPower:
		GO99 H 5
		GO99 I 5 A_PlaySound("KNI_HIT")
	DeathFall:
		GO99 J 5 A_Fall
		GO99 KLM 5
		TNT1 A 0 A_Jump(230,"Regenerating")
		GO99 N -1
		Stop
	Regenerating:
		GO99 NNNNNNNNNNNNNNN 70
		GO99 N -1 Thing_Raise(0)
		Wait
	Splode:
		TNT1 A 0 A_XScream
		TNT1 A 0 A_NoBlocking
		XXLF O 5 A_SpawnItem("AeniBlood",0,32,0)
		TNT1 A 0 A_SpawnItemEx ("AeniBlood", 0,34,0, random(-75,75)*0.01,random(-75,75)*0.01,random(-65,65)*0.01, 0, SXF_NOCHECKPOSITION|SXF_CLIENTSIDE|SXF_TRANSFERTRANSLATION, 0)	
		XXLF P 5 A_SpawnItem("AeniBlood",0,34,0)
		XXLF Q 5 A_SpawnItem("AeniBlood",0,26,0)
		XXLF R 5 A_SpawnItem("AeniBlood",0,20,0)
		XXLR S 5 A_SpawnItem("AeniBlood",0,14,0)
		XXLR T 5 A_SpawnItem("AeniBlood",0,6,0)
		XXLR U 5 A_SpawnItem("AeniBlood",0,0,0)
		XXLR V 5 A_SpawnItem("AeniBlood",0,0,0)
		XXLR W 5 // No extra blood needed here. He's dead, Jim.
		TNT1 A 0 A_SpawnItemEx("DEADStrikeKnightGib",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
		Stop
	Death.Fire2:
		TNT1 A 1 A_SpawnItem("BurnDeath1", 2, 1)
		TNT1 A 0 A_AlertMonsters
		Stop
	Death.head:
		GO99 O 9 A_PlaySound("headshot/splat")
		TNT1 A 0 A_SpawnItem("AeniBlood",0,40,0)
		TNT1 A 0 A_SpawnItem("AeniBlood",0,40,0)
		GO99 P 9 A_XScream
		GO99 Q 9 A_Fall
		GO99 R 5
		GO99 S 5 A_SpawnItemEx("DEADInitiate",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
		Stop
	Raise: 
		GO99 KJI 5 //A_JumpIfInventory("traitor",1,"RaiseTraitor")
		GO99 H 5 Thing_SetSpecial(0,226,642,0,random(8,10))
		Goto See
	} 
}

actor LeapTimer : inventory { Inventory.MaxAmount "140" }