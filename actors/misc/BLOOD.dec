//written from scratch by Aenima, for Avi
//sprites and some code originally from Super Skulltag
//loosely based on F.E.A.R.
//hi-res BLUD sprites by Bloax


ACTOR AeniBlood : Blood replaces Blood
{
  SpawnID 130
  Mass 5
  +NOBLOCKMAP
  +NOTELEPORT
  -ALLOWPARTICLES
  +NOGRAVITY
  Scale 0.25
  Alpha 0.5
  States
  {
  Spawn:
  Spray:
    BLOD C 0
    BLOD C 0 A_SpawnItemEx ("AeniBloodEffectSpawner", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION|SXF_CLIENTSIDE|SXF_TRANSFERTRANSLATION, 0)
    BLOD C 0 A_SpawnItemEx ("AeniBlood2", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION|SXF_CLIENTSIDE|SXF_TRANSFERTRANSLATION, 0)
    Stop
  }
}

ACTOR AeniBlood2 : AeniBlood
{
  Scale 0.4
  alpha 1.0
  +CLIENTSIDEONLY
  States
  {
  Spawn:
    BLOD A 0
    BLOD AAA 1 A_Jump(128,"Spawn2")
	
  Spawn2:
	BLOD A 0 A_PlaySound("flesh/hit")
    BLOD AAABBBCCCCCC 1 A_Fadeout(0.08)
    Stop
  }
}


ACTOR AeniBloodEffectSpawner
{
  +NOTELEPORT
  Radius 1
  Height 1
  +NOBLOCKMAP
  +NOTELEPORT
  +NOGRAVITY
  +NOCLIP
  -SOLID
  -SHOOTABLE
  +DONTSPLASH
  RENDERSTYLE NONE
  States
  {
  Spawn:
    TNT1 A 0
      TNT1 AAAA 0 Bright A_SpawnItemEx ("BludEffect1", 0,0,0, random(-65,65)*0.01,random(-65,65)*0.01,random(-65,65)*0.01, 0, SXF_NOCHECKPOSITION|SXF_CLIENTSIDE|SXF_TRANSFERTRANSLATION, 0)
      BLOD A 0 A_Jump(144, "Death")
      TNT1 AAAA 0 Bright A_SpawnItemEx ("BludEffect1b", 0,0,0, random(-30,30)*0.01,random(-30,30)*0.01,random(-30,30)*0.01, 0, SXF_NOCHECKPOSITION|SXF_CLIENTSIDE|SXF_TRANSFERTRANSLATION, 0)
	  BLOD A 0 A_Jump(128, "Death")
	  
	  BLOD A 0 A_PlaySound("flesh/gush")
      TNT1 AAAA 1 Bright A_CustomMissile("BludEffect3",0,0,random(-180,180),2,random(-90,45))
	  BLOD A 0 A_Jump(64, "Death")
	  BLOD A 0 A_PlaySound("flesh/gush")
      TNT1 AAAA 1 Bright A_CustomMissile("BludEffect4",0,0,random(-180,180),2,random(-90,60))
	  BLOD A 0 A_Jump(32, "Death")
	  BLOD A 0 A_PlaySound("flesh/gush")
      TNT1 AAAA 1 Bright A_CustomMissile("BludEffect2",0,0,random(-180,180),2,random(-90,75))
      Stop
  Death:
    TNT1 AA 0
	Stop
  }
}


ACTOR BludEffect1
{
	+NOTELEPORT
  Radius 1
  Height 1
  Speed 0//1
  Damage 0
  Projectile
  -RANDOMIZE
  -NOCLIP
  +THRUACTORS
  +DONTSPLASH
  +NOPAIN
  +PAINLESS
  RenderStyle Translucent
  Alpha 0.51
  scale 0.75
  States
  {
  Spawn:
    BLOD A 0
    BLOD A 0 A_Jump(64, "Death")
	
  Prefade:
    BLOD A 0 A_Jump(32, "WhichSpawn")
    BLOD A 0 A_FadeOut(0.01)
    Loop
	
  WhichSpawn:
    BLOD A 0 A_Jump(96, "Spawn2", "Spawn3")
	
  Spawn1:
    BLOD AAAABBBBCCCCCCCC 2 A_FadeOut(0.04)
    stop	
  Spawn2:
    BLOD AAABBBCCCCCCC 2 A_FadeOut(0.05)
    stop	
  Spawn3:
    BLOD AABBCCCCCC 2 A_FadeOut(0.06)
    stop
	
  Death:
  XDeath:
  Crash:
    BLOD C 0 A_Stop
    BLOD C 1 A_FadeOut(0.15)
	Loop
  }
}

ACTOR BludEffect1b : BludEffect1
{
  Alpha 0.51
  scale 1.25
  States
  {
  Spawn:
    BLOD A 0
    BLOD A 0 A_Jump(64, "Death")
	
  Prefade:
    BLOD A 0 A_Jump(20, "WhichSpawn")
    BLOD A 0 A_FadeOut(0.01)
    Loop	


  WhichSpawn:
    BLOD ABC 1 A_FadeOut(0.07)
    BLOD A 0 A_Jump(128, "Spawn2", "Spawn3")
	
  Spawn1:
    BLOD C 3 A_FadeOut(0.01)
    BLOD C 0 A_Jump(10, "Death")
	Loop
  Spawn2:
    BLOD C 2 A_FadeOut(0.01)
    BLOD C 0 A_Jump(6, "Death")
	Loop
  Spawn3:
    BLOD C 1 A_FadeOut(0.01)
    BLOD C 0 A_Jump(2, "Death")
	Loop
	
  Death:
  XDeath:
  Crash:
    BLOD C 0 A_Stop
    BLOD C 1 A_FadeOut(0.15)
	Loop
  }
}

ACTOR BludEffect2 : BludEffect1
{
  Speed 4
  -NOGRAVITY
  Gravity 0.5
  Alpha 0.85
  scale 0.3
}

ACTOR BludEffect3 : BludEffect1
{
  Speed 8
  -NOGRAVITY
  Gravity 0.5
  Alpha 0.85
  scale 0.1
}

ACTOR BludEffect4 : BludEffect1
{
  Speed 6
  -NOGRAVITY
  Gravity 0.5
  Alpha 0.85
  scale 0.2
}
