ACTOR FShockKnight : ShockKnight 11820 {
	//$Category Federation

	Health 450
	DamageFactor "KillMe", 0.0
	DamageFactor "FriendBullet", 0.5
	+NOBLOCKMONST
	+FRIENDLY

	States {

/******************************************************************************\
==================================MAIN STATES===================================
\******************************************************************************/

	Spawn:
		SKFP A 0
		/*TNT1 A 0 A_CustomMissile("OrbitFriendly",44,0,0,2,0)
		TNT1 A 0 A_CustomMissile("OrbitFriendly2",44,0,180,2,0)*/
		TNT1 A 0 A_ChangeFlag("FRIENDLY",1)
		TNT1 A 0 ACS_ExecuteAlways(556,0)
		TNT1 A 0 Thing_SetSpecial(0,226,613,0,0)
		TNT1 A 0 A_SpawnItem ("KillMe")
	SpawnLoop:
		TNT1 A 0 A_SpawnItem("HeadshotTarget10s",15,40,0)
		SKFP E 10 A_LookEx(0,0,random(512,2048),random(1024,4096),random(120,180),"See")
		Loop
	See:
		SKFP A 1 // Infinite loop crash prevention?
		TNT1 A 0 A_CheckSight("ChanceToCharge")
		Goto TakeCharge
	ChanceToCharge:
		TNT1 A 0 A_Jump(128,"GiveCharge")
		TNT1 A 0 A_TakeInventory("DealWithIt",1)
		Goto Chase
	GiveCharge:
		TNT1 A 0 A_JumpIfInventory("KnightCharge",1,"TakeCharge")
		TNT1 A 0 A_GiveInventory("KnightCharge",1)
		Goto IfLowHealth
	TakeCharge:
		TNT1 A 0 A_TakeInventory("KnightCharge",1)
		Goto IfLowHealth
	IfLowHealth:
		TNT1 A 0 A_JumpIfHealthLower(50,"LowHealth")
		TNT1 A 0 A_TakeInventory("DealWithIt",1)
		Goto HealthHigh
	HealthHigh:
		TNT1 A 0 A_ChangeFlag("Frightened",0)
		Goto Chase
	LowHealth:
		TNT1 A 0 A_Jump(32,"HealUp")
		TNT1 A 0 A_TakeInventory("DealWithIt",1)
		Goto Retreat
	Retreat:
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_ChangeFlag("Frightened",1)
		Goto HealUp
	HealUp:
		TNT1 A 0 HealThing(1)
		Goto Chase
	Chase:
		TNT1 A 0 A_JumpIfInventory("KnightCharge",1,"Charge")
		TNT1 A 0 A_SpawnItem("HeadshotTarget3s",15,40,0)
		SKFP A 3 A_FastChase
		TNT1 A 0 A_SpawnItem("HeadshotTarget3s",15,40,0)
		SKFP B 3 A_Chase
		TNT1 A 0 A_SpawnItem("HeadshotTarget3s",15,40,0)
		SKFP C 3 A_Chase
		TNT1 A 0 A_SpawnItem("HeadshotTarget3s",15,40,0)
		SKFP D 3 A_Chase
		Goto See
	Charge:
		TNT1 A 0 A_SpawnItem("HeadshotTarget3s",15,40,0)
		TNT1 A 0 A_Chase
		SKFP A 3 A_Chase
		TNT1 A 0 A_SpawnItem("HeadshotTarget3s",15,40,0)
		TNT1 A 0 A_Chase
		SKFP B 3 A_Chase
		TNT1 A 0 A_SpawnItem("HeadshotTarget3s",15,40,0)
		TNT1 A 0 A_Chase
		SKFP C 3 A_Chase
		TNT1 A 0 A_SpawnItem("HeadshotTarget3s",15,40,0)
		TNT1 A 0 A_Chase
		SKFP D 3 A_Chase
		Goto See

/******************************************************************************\
=================================ATTACK STATES==================================
\******************************************************************************/

	Missile:
		TNT1 A 0 A_SpawnItem("HeadshotTarget10s",0,40,0)
		SKFP E 10 A_FaceTarget
		TNT1 A 0 A_Jump(64,"DistanceCheck")
		Goto BulletAttack
	BulletAttack:
		TNT1 A 0 A_JumpIfInventory("KnightAmmo",30,"Reload")
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("FNS_F1",6)
		TNT1 A 0 A_GiveInventory("KnightAmmo",1)
		TNT1 A 0 A_SpawnItem("HeadshotTarget1s",0,40,0)
		//TNT1 A 0 A_CustomBulletAttack(5,5,1,random(1,3),"FBulletPuff")
		SKFP F 1 BRIGHT A_CustomMissile("FIceTracer",38,0,random(-5,5),0,random(-5,5))
		SKFP E 2 A_SpawnItem("HeadshotTarget2s",0,40,0)
		TNT1 A 0 A_JumpIfInventory("KnightAmmo",40,"Reload")
		TNT1 A 0 A_MonsterRefire(224,"See")
		Loop
	Reload:
		TNT1 A 0 A_ChangeFlag(NOPAIN,1)
		TNT1 A 0 A_TakeInventory("KnightAmmo",30)
		SKFP E 3 A_SpawnItem("HeadshotTarget3s",0,40,0)
		TNT1 A 0 A_PlaySound("FNS_R2")
		SKFP E 2 A_SpawnItem("HeadshotTarget2s",0,40,0)
		SKFP EEEEEEEEEEEEEEEEEEEEEEE 4 A_SpawnItem("HeadshotTarget4s",0,40,0)
		TNT1 A 0 A_ChangeFlag(NOPAIN,0)
		Goto See
	DistanceCheck:
		TNT1 A 0 A_JumpIfCloser(1768,"CloseRangeSpecial")
		TNT1 A 0 A_TakeInventory("DealWithIt",1)
		Goto BulletAttack
	CloseRangeSpecial:
		TNT1 A 0 A_SpawnItem("HeadshotTarget4s",0,40,0)
		SKFP E 4 A_FaceTarget
		TNT1 A 0 A_JumpIfCloser(192,"KnifeLunge")
		Goto LaunchShockGrenade
	LaunchShockGrenade:
		SKFP F 2 BRIGHT A_CustomMissile("KnightShockGrenadeShell",28,5,random(-15,15),2,random(0,10))
		Goto LaunchGrenadeFinish
	LaunchGrenadeFinish:
		SKFP EE 3 A_SpawnItem("HeadshotTarget3s",0,40,0)
		Goto GrenadeReload
	GrenadeReload:
		TNT1 A 0 A_ChangeFlag(NOPAIN,1)
		TNT1 A 0 A_PlaySoundEx("M4_CLIPO","SoundSlot5")
		SKFP E 2 A_SpawnItem("HeadshotTarget2s",0,40,0)
		SKFP E 10 A_SpawnItem("HeadshotTarget10s",0,40,0)
		TNT1 A 0 A_PlaySoundEx("M4_CLIPI","SoundSlot5")
		SKFP E 2 A_SpawnItem("HeadshotTarget2s",0,40,0)
		SKFP EEEEEE 10 A_SpawnItem("HeadshotTarget10s",0,40,0)
		TNT1 A 0 A_ChangeFlag(NOPAIN,0)
		Goto See
	KnifeLunge:
		TNT1 A 0 ThrustThing(angle*256/360,25,0,0)
		TNT1 A 0 A_SpawnItem("HeadshotTarget4s",0,40,0)
		SKFP E 4 A_FaceTarget
		TNT1 A 0 A_PlaySound("knife/swing")
		TNT1 A 0 A_SpawnItem("HeadshotTarget2s",0,40,0)
		SKFP E 2 A_CustomMeleeAttack(34,"knife/swing","knife/swing","Knife",1)
		Goto CloseRangeFinish
	CloseRangeFinish:
		SKFP EE 3 A_SpawnItem("HeadshotTarget3s",0,40,0)
		Goto See

/******************************************************************************\
==================================PAIN STATES===================================
\******************************************************************************/

	Pain:
		SKFP G 3 A_SpawnItem("HeadshotTarget4s",0,40,0)
		TNT1 A 0 A_SpawnItem("HeadshotTarget4s",0,40,0)
		SKFP G 3 A_Pain
		Goto See
	Pain.Shock:
		TNT1 A 0 A_ChangeFlag(NOPAIN,1)
		TNT1 A 0 A_Pain
		SKFP GGGGGGGGGGGGGGGGGGGGGGGGGGGGGG 10 A_SpawnItem("HeadshotTarget10s",0,40,0)
		TNT1 A 0 A_ChangeFlag(NOPAIN,0)
		Goto See
	Pain.WakizashiSlash:
	Pain.WakizashiStab:
		TNT1 A 0 bright A_CustomMissile("ElectricShock",28,0,0)
		TNT1 A 0 bright A_CustomMissile("ElectricShock",28,0,45)
		TNT1 A 0 bright A_CustomMissile("ElectricShock",28,0,90)
		TNT1 A 0 bright A_CustomMissile("ElectricShock",28,0,135)
		TNT1 A 0 bright A_CustomMissile("ElectricShock",28,0,180)
		TNT1 A 0 bright A_CustomMissile("ElectricShock",28,0,-135)
		TNT1 A 0 bright A_CustomMissile("ElectricShock",28,0,-90)
		TNT1 A 0 bright A_CustomMissile("ElectricShock",28,0,-45)
		SKFP G 3 A_SpawnItem("HeadshotTarget3s",0,40,0)
		TNT1 A 0 A_SpawnItem("HeadshotTarget3s",0,40,0)
		SKFP G 3 A_Pain
		Goto See

/******************************************************************************\
==================================DEATH STATES==================================
\******************************************************************************/

	Death:
		TNT1 A 0 A_JumpIfHealthLower(-60,"Splode")
	Death.KnifeSlash:
	Death.KnifeStab:
	Death.KnifeSlashPower:
	Death.KnifeStabPower:
	Death.WakizashiSlash:
	Death.WakizashiStab:
	Death.WakizashiSlashPower:
	Death.WakizashiStabPower:
		SKFP H 5 A_AlertMonsters
		SKFP I 5 A_Scream
		SKFP J 5 A_Fall
		SKFP KLM 5
		TNT1 A 0 A_Jump(5,"Regenerating")
		TNT1 A 0 A_GiveInventory("isDead",1)
		SKFP N -1
		Stop
	Regenerating:
		SKFP NNNNNNNNNNNNNNN 70
		SKFP N -1 Thing_Raise(0)
		Wait
	Splode:
		SKFP I 1 A_SpawnDebris("NashGore_Gib9",0)
		TNT1 A 0 A_SpawnDebris("NashGore_Gib10",0)
		TNT1 A 0 A_SpawnDebris("NashGore_Gib11",0)
		TNT1 A 0 A_SpawnDebris("NashGore_Gib11",0)
		TNT1 A 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,0)
		TNT1 A 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,0)
		TNT1 A 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,0)
		TNT1 A 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,0)
		TNT1 A 0 A_XScream
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_GiveInventory("isDead",1)
		TNT1 A 0 A_SpawnItemEx("DEADFStrikeKnightHead",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
		Stop
	Death.Fire2:
		TNT1 A 0 A_GiveInventory("isDead",1)
		TNT1 A 1 A_SpawnItem("BurnDeath1", 2, 1)
		TNT1 A 0 A_AlertMonsters
		Stop
	Death.head:
		SKFP O 9 A_PlaySound("headshot/splat")
		TNT1 A 0 A_SpawnItem("AeniBlood",0,40,0)
		TNT1 A 0 A_SpawnItem("AeniBlood",0,40,0)
		SKFP P 9 A_XScream
		SKFP Q 9 A_Fall
		SKFP R 5 A_GiveInventory("isDead",1)
		SKFP S 5 A_SpawnItemEx("DEADFStrikeKnight",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
		Stop
	Raise:
		SKFP KJI 5
		TNT1 A 0 A_ChangeFlag("FRIENDLY",1)
		TNT1 A 0 A_TakeInventory("isDead",1)
		/*TNT1 A 0 A_CustomMissile("OrbitFriendly",44,0,0,2,0)
		TNT1 A 0 A_CustomMissile("OrbitFriendly2",44,0,180,2,0)*/
		SKFP H 5 Thing_SetSpecial(0,226,613,0,0)
		Goto See
	}
}