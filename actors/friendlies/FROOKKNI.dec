ACTOR FRookKnight : RookKnight 11942 {
	//$Category Federation
	Health 940
	DamageFactor "KillMe", 0.0
	DamageFactor "FriendBullet", 0.5
	+NOBLOCKMONST
	+FRIENDLY
	States {
	
/******************************************************************************\
==================================MAIN STATES===================================
\******************************************************************************/
		
	Spawn:
		G99P A 0
		TNT1 A 0 A_ChangeFlag("FRIENDLY",1)
		TNT1 A 0 ACS_ExecuteAlways(556,0)
		TNT1 A 0 Thing_SetSpecial(0,226,613,0,0) // EXP/KIA SCRIPT
		TNT1 A 0 A_SpawnItem ("KillMe")
	SpawnLoop:
		TNT1 A 0 A_SpawnItem ("HeadshotTarget10", 15, 40,0)
		G99P E 10 A_Look
		Goto SpawnLoop
	See:
		G99P A 1 // Infinite loop crash prevention?
		TNT1 A 0 A_CheckSight("ChanceToCharge")
		Goto TakeCharge
	ChanceToCharge:
		TNT1 A 0 A_Jump(128,"GiveCharge")
		TNT1 A 0 A_TakeInventory("DealWithIt",1)
		Goto Chase
	GiveCharge:
		TNT1 A 0 A_JumpIfInventory("KnightCharge",1,"TakeCharge")
		TNT1 A 0 A_GiveInventory("KnightCharge",1)
		Goto IfLowHealth
	TakeCharge:
		TNT1 A 0 A_TakeInventory("KnightCharge",1)
		Goto IfLowHealth
	IfLowHealth:
		TNT1 A 0 A_JumpIfHealthLower(50,"LowHealth")
		TNT1 A 0 A_TakeInventory("DealWithIt",1)
		Goto HealthHigh
	HealthHigh:
		TNT1 A 0 A_ChangeFlag("Frightened",0)
		Goto Chase
	LowHealth:
		TNT1 A 0 A_Jump(32,"HealUp")
		TNT1 A 0 A_TakeInventory("DealWithIt",1)
		Goto Retreat
	Retreat:
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_ChangeFlag("Frightened",1)
		Goto HealUp
	HealUp:
		TNT1 A 0 HealThing(1)
		Goto Chase
	Chase:
		TNT1 A 0 A_JumpIfInventory("KnightCharge",1,"Charge")
		TNT1 A 0 A_SpawnItem("HeadshotTarget4",15,40,0)
		G99P A 4 A_FastChase
		TNT1 A 0 A_SpawnItem("HeadshotTarget4",15,40,0)
		G99P B 4 A_Chase
		TNT1 A 0 A_SpawnItem("HeadshotTarget4",15,40,0)
		G99P C 4 A_Chase
		TNT1 A 0 A_SpawnItem("HeadshotTarget4",15,40,0)
		G99P D 4 A_Chase
		Goto See
	Charge:
		TNT1 A 0 A_SpawnItem("HeadshotTarget4",15,40,0)
		TNT1 A 0 A_Chase
		G99P A 4 A_Chase
		TNT1 A 0 A_SpawnItem("HeadshotTarget4",15,40,0)
		TNT1 A 0 A_Chase
		G99P B 4 A_Chase
		TNT1 A 0 A_SpawnItem("HeadshotTarget4",15,40,0)
		TNT1 A 0 A_Chase
		G99P C 4 A_Chase
		TNT1 A 0 A_SpawnItem("HeadshotTarget4",15,40,0)
		TNT1 A 0 A_Chase
		G99P D 4 A_Chase
		Goto See
		
/******************************************************************************\
=================================ATTACK STATES==================================
\******************************************************************************/
		
	Missile:
		TNT1 A 0 A_SpawnItem("HeadshotTarget10",0,40,0)
		G99P E 10 A_FaceTarget
		TNT1 A 0 A_Jump(64,"DistanceCheck")
		Goto BulletAttack
	BulletAttack:
		TNT1 A 0 A_JumpIfInventory("KnightAmmo",30,"Reload")
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySoundEx("MP7/FIRE","SoundSlot6")
		TNT1 A 0 A_GiveInventory("KnightAmmo",1)
		TNT1 A 0 A_SpawnItem("HeadshotTarget1",0,40,0)
		G99P E 1 BRIGHT A_CustomMissile("RookTracer",38,0,random(-5,5),0,random(-5,5))
		G99P E 2 A_SpawnItem("HeadshotTarget2",0,40,0)
		TNT1 A 0 A_JumpIfInventory("KnightAmmo",30,"Reload")
		TNT1 A 0 A_MonsterRefire(224,"See")
		Goto Loop
	Reload:
		TNT1 A 0 A_ChangeFlag(NOPAIN,1)
		TNT1 A 0 A_TakeInventory("KnightAmmo",30)
		G99P EE 3 A_SpawnItem("HeadshotTarget3",0,40,0)
		TNT1 A 0 A_PlaySound("FNS_R2")
		G99P E 2 A_SpawnItem("HeadshotTarget2",0,40,0)
		G99P EEEEEEEEEEEEEEEEEEEEEEE 4 A_SpawnItem("HeadshotTarget4",0,40,0)
		TNT1 A 0 A_ChangeFlag(NOPAIN,0)
		goto See
	DistanceCheck:
		TNT1 A 0 A_JumpIfCloser(768,"ShortRangeBombard")
		TNT1 A 0 A_TakeInventory("DealWithIt",1)
		Goto LongRangeBombard
	LongRangeBombard:
		TNT1 A 0 A_ChangeFlag(NOPAIN,1)
		G99P E 8 A_FaceTarget
		TNT1 A 0 A_SpawnItemEx("ShieldSpawner",0,96,0,0,0,0,0,32)
		TNT1 A 0 A_SpawnItemEx("ShieldSpawner",0,96,0,0,0,0,90,32)
		TNT1 A 0 A_SpawnItemEx("ShieldSpawner",0,96,0,0,0,0,180,32)
		TNT1 A 0 A_SpawnItemEx("ShieldSpawner",0,96,0,0,0,0,-90,32)
		TNT1 A 0 A_PlaySound("monster/Spreadsh")
		TNT1 A 0 A_SpawnItem("HeadshotTarget4",0,40,0)
		G99P F 4 BRIGHT A_CustomMissile("RookKnightArtillery1",48,10,0,2,10)
		TNT1 A 0 A_PlaySound("monster/Spreadsh")
		TNT1 A 0 A_SpawnItem("HeadshotTarget4",0,40,0)
		G99P F 4 BRIGHT A_CustomMissile("RookKnightArtillery2",48,-10,0,2,11)
		TNT1 A 0 A_PlaySound("monster/Spreadsh")
		TNT1 A 0 A_SpawnItem("HeadshotTarget4",0,40,0)
		G99P F 4 BRIGHT A_CustomMissile("RookKnightArtillery3",48,10,0,2,12)
		TNT1 A 0 A_PlaySound("monster/Spreadsh")
		TNT1 A 0 A_SpawnItem("HeadshotTarget4",0,40,0)
		G99P F 4 BRIGHT A_CustomMissile("RookKnightArtillery4",48,-10,0,2,13)
		TNT1 A 0 A_PlaySound("monster/Spreadsh")
		TNT1 A 0 A_SpawnItem("HeadshotTarget4",0,40,0)
		G99P F 4 BRIGHT A_CustomMissile("RookKnightArtillery5",48,10,0,2,14)
		TNT1 A 0 A_PlaySound("monster/Spreadsh")
		TNT1 A 0 A_SpawnItem("HeadshotTarget4",0,40,0)
		G99P F 4 BRIGHT A_CustomMissile("RookKnightArtillery6",48,-10,0,2,15)
		TNT1 A 0 A_PlaySound("monster/Spreadsh")
		TNT1 A 0 A_SpawnItem("HeadshotTarget4",0,40,0)
		G99P F 4 BRIGHT A_CustomMissile("RookKnightArtillery7",48,10,0,2,16)
		TNT1 A 0 A_PlaySound("monster/Spreadsh")
		TNT1 A 0 A_SpawnItem("HeadshotTarget4",0,40,0)
		G99P F 4 BRIGHT A_CustomMissile("RookKnightArtillery8",48,-10,0,2,17)
		TNT1 A 0 A_SpawnItem("HeadshotTarget4",0,40,0)
		G99P E 4 A_FaceTarget
		TNT1 A 0 A_ChangeFlag(NOPAIN,0)
		Goto See
	ShortRangeBombard:
		TNT1 A 0 A_CustomMissile("MiniBomb",48,10,Random(-45,45),2,Random(0,45))
		TNT1 A 0 A_CustomMissile("MiniBomb",48,-10,Random(-45,45),2,Random(0,45))
		TNT1 A 0 A_CustomMissile("MiniBomb",48,10,Random(-45,45),2,Random(0,45))
		TNT1 A 0 A_CustomMissile("MiniBomb",48,-10,Random(-45,45),2,Random(0,45))
		TNT1 A 0 A_CustomMissile("MiniBomb",48,10,Random(-45,45),2,Random(0,45))
		TNT1 A 0 A_CustomMissile("MiniBomb",48,-10,Random(-45,45),2,Random(0,45))
		TNT1 A 0 A_CustomMissile("MiniBomb",48,10,Random(-45,45),2,Random(0,45))
		G99P E 5 BRIGHT A_CustomMissile("MiniBomb",48,-10,Random(-45,45),2,Random(0,45))
		Goto See
		
/******************************************************************************\
==================================PAIN STATES===================================
\******************************************************************************/
		
	Pain:
		G99P G 3 A_SpawnItem("HeadshotTarget4",0,40,0)
		TNT1 A 0 A_SpawnItem("HeadshotTarget4",0,40,0)
		G99P G 3 A_Pain
		goto See
	Pain.Shock:
	Pain.ShockSlug:
		G99P G 2 A_ChangeFlag(NOPAIN,1)
		TNT1 A 0 A_Pain
		G99P GGGGGGGGGGGGGGGGGGGGGGGGGGGGGG 10 A_SpawnItem("HeadshotTarget10",0,40,0)
		TNT1 A 0 A_ChangeFlag(NOPAIN,0)
		Goto See
	Pain.KillMe:
		Goto See
		
/******************************************************************************\
==================================DEATH STATES==================================
\******************************************************************************/
		
	Death: 
		TNT1 A 0 A_JumpIfHealthLower(-60,"Splode")
		G99P H 5 A_AlertMonsters
		G99P I 5 A_Scream
		Goto DeathFall
	Death.KnifeSlash:
	Death.KnifeStab:
	Death.KnifeSlashPower:
	Death.KnifeStabPower:
	Death.WakizashiSlash:
	Death.WakizashiStab:
	Death.WakizashiSlashPower:
	Death.WakizashiStabPower:
		G99P H 5
		G99P I 5 A_PlaySound("KNI_HIT")
	DeathFall:
		G99P J 5 A_Fall
		G99P KLM 5
		G99P N 0 A_Jump(5,"Regenerating")
		G99P N -1
		stop
	Regenerating:
		G99P NNNNNNNNNNNNNNN 70
		G99P N -1 Thing_Raise(0)
		Wait
	Splode:
		TNT1 A 0 A_XScream
		TNT1 A 0 A_NoBlocking
		G99P O 5 A_SpawnItem("AeniBlood",0,32,0)
		TNT1 A 0 A_SpawnItemEx("AeniBlood",0,34,0,random(-75,75)*0.01,random(-75,75)*0.01,random(-65,65)*0.01,0,SXF_NOCHECKPOSITION|SXF_CLIENTSIDE|SXF_TRANSFERTRANSLATION,0)	
		G99P P 5 A_SpawnItem("AeniBlood",0,34,0)
		G99P Q 5 A_SpawnItem("AeniBlood",0,26,0)
		G99P R 5 A_SpawnItem("AeniBlood",0,20,0)
		G99P S 5 A_SpawnItem("AeniBlood",0,14,0)
		G99P T 5 A_SpawnItem("AeniBlood",0,6,0)
		G99P U 5 A_SpawnItem("AeniBlood",0,0,0)
		G99P V 5 A_SpawnItem("AeniBlood",0,0,0)
		G99P W 5 // No extra blood needed here. He's dead, Jim.
		TNT1 A 0 A_SpawnItemEx("DEADStrikeKnightGib",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
		stop
	Death.Fire2:
		TNT1 A 1 A_SpawnItem("BurnDeath1",2,1)
		TNT1 A 0 A_AlertMonsters
		stop
	Death.head:
		G99P O 9 A_PlaySound("headshot/splat")
		TNT1 A 0 A_SpawnItem("AeniBlood",0,40,0)
		TNT1 A 0 A_SpawnItem("AeniBlood",0,40,0)
		G99P P 9 A_XScream
		G99P Q 9 A_Fall
		G99P RS 5
		TNT1 A 0 A_SpawnItemEx("DEADInitiate",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
		stop
	Raise:
		G99P KJIH 5
		Goto See
	}
}