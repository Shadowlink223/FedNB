ACTOR FIceKnight : IceKnight 11970 {
	//$Category Federation

	Health 450
	DamageFactor "KillMe", 0.0
	DamageFactor "FriendBullet", 0.5
	+NOBLOCKMONST
	+FRIENDLY

	States {

/******************************************************************************\
==================================MAIN STATES===================================
\******************************************************************************/

	Spawn:
		SKFP A 0
		/*TNT1 A 0 A_CustomMissile("OrbitFriendly",44,0,0,2,0)
		TNT1 A 0 A_CustomMissile("OrbitFriendly2",44,0,180,2,0)*/
		TNT1 A 0 A_ChangeFlag("FRIENDLY",1)
		TNT1 A 0 ACS_ExecuteAlways(556,0)
		TNT1 A 0 Thing_SetSpecial(0,226,613,0,0)
		TNT1 A 0 A_SpawnItem ("KillMe")
	SpawnLoop:
		TNT1 A 0 A_SpawnItem("HeadshotTarget10i",15,40,0) //Why does removing the space before the parenthesis without this comment turn this into a gfx?
		SKFP E 10 A_LookEx(0,0,random(512,2048),random(1024,4096),random(120,180),"See")
		Loop
	See:
		SKFA P 1 // Infinite loop crash prevention?
		TNT1 A 0 A_CheckSight("ChanceToCharge")
		Goto TakeCharge
	ChanceToCharge:
		TNT1 A 0 A_Jump(128,"GiveCharge")
		TNT1 A 0 A_TakeInventory("DealWithIt",1)
		Goto Chase
	GiveCharge:
		TNT1 A 0 A_JumpIfInventory("KnightCharge",1,"TakeCharge")
		TNT1 A 0 A_GiveInventory("KnightCharge",1)
		Goto IfLowHealth
	TakeCharge:
		TNT1 A 0 A_TakeInventory("KnightCharge",1)
		Goto IfLowHealth
	IfLowHealth:
		TNT1 A 0 A_JumpIfHealthLower(50,"LowHealth")
		TNT1 A 0 A_TakeInventory("DealWithIt",1)
		Goto HealthHigh
	HealthHigh:
		TNT1 A 0 A_ChangeFlag("Frightened",0)
		Goto Chase
	LowHealth:
		TNT1 A 0 A_Jump(32,"HealUp")
		TNT1 A 0 A_TakeInventory("DealWithIt",1)
		Goto Retreat
	Retreat:
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_ChangeFlag("Frightened",1)
		Goto HealUp
	HealUp:
		TNT1 A 0 HealThing(1)
		Goto Chase
	Chase:
		TNT1 A 0 A_JumpIfInventory("KnightCharge",1,"Charge")
		TNT1 A 0 A_SpawnItem("HeadshotTarget3i",15,40,0)
		SKFP A 3 A_FastChase
		TNT1 A 0 A_SpawnItem("HeadshotTarget3i",15,40,0)
		SKFP B 3 A_Chase
		TNT1 A 0 A_SpawnItem("HeadshotTarget3i",15,40,0)
		SKFP C 3 A_Chase
		TNT1 A 0 A_SpawnItem("HeadshotTarget3i",15,40,0)
		SKFP D 3 A_Chase
		Goto See
	Charge:
		TNT1 A 0 A_SpawnItem("HeadshotTarget3i",15,40,0)
		TNT1 A 0 A_Chase
		SKFP A 3 A_Chase
		TNT1 A 0 A_SpawnItem("HeadshotTarget3i",15,40,0)
		TNT1 A 0 A_Chase
		SKFP B 3 A_Chase
		TNT1 A 0 A_SpawnItem("HeadshotTarget3i",15,40,0)
		TNT1 A 0 A_Chase
		SKFP C 3 A_Chase
		TNT1 A 0 A_SpawnItem("HeadshotTarget3i",15,40,0)
		TNT1 A 0 A_Chase
		SKFP D 3 A_Chase
		Goto See

/******************************************************************************\
=================================ATTACK STATES==================================
\******************************************************************************/

	Missile:
		TNT1 A 0 A_SpawnItem("HeadshotTarget10i",0,40,0)
		SKFP E 10 A_FaceTarget
		TNT1 A 0 A_Jump(64,"DistanceCheck")
		Goto BulletAttack
	BulletAttack:
		TNT1 A 0 A_Jump(192,"CryoAttack")
		TNT1 A 0 A_JumpIfInventory("KnightAmmo",30,"Reload")
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("FNS_F1",6)
		TNT1 A 0 A_GiveInventory("KnightAmmo",1)
		TNT1 A 0 A_SpawnItem("HeadshotTarget1i",0,40,0)
		//TNT1 A 0 A_CustomBulletAttack(5,5,1,random(1,3),"FBulletPuff")
		SKFP F 1 BRIGHT A_CustomMissile("FIceTracer",38,0,random(-5,5),0,random(-5,5))
		SKFP E 2 A_SpawnItem("HeadshotTarget2i",0,40,0)
		TNT1 A 0 A_JumpIfInventory("KnightAmmo",40,"Reload")
		TNT1 A 0 A_MonsterRefire(224,"See")
		Loop
	CryoAttack:
		TNT1 A 0 A_JumpIfInventory("loadedOPG_Cryo",10,1)
		Goto CryoNeedle
		TNT1 A 0 A_Jump(64,"CryoBall")
		Goto CryoNeedle
	CryoNeedle:
		TNT1 A 0 A_JumpIfInventory("loadedOPG_Cryo",0,"CryoReload")
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_GiveInventory("loadedOPG_Cryo",1)
		TNT1 A 0 A_SpawnItem("HeadshotTarget1i",0,40,0)
		SKFP F 1 BRIGHT A_CustomMissile("DPP_Cryo",28,0,random(-1,1),2,random(-1,1))
		SKFP E 2 A_SpawnItem("HeadshotTarget2i",0,40,0)
		TNT1 A 0 A_JumpIfInventory("loadedOPG_Cryo",0,"CryoReload")
		TNT1 A 0 A_MonsterRefire(224,"See")
		Loop
	CryoBall:
		TNT1 A 0 A_JumpIfInventory("loadedOPG_Cryo",0,"CryoReload")
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_GiveInventory("loadedOPG_Cryo",10)
		TNT1 A 0 A_SpawnItem("HeadshotTarget1i",0,40,0)
		SKFP F 1 BRIGHT A_CustomMissile("CryoBlast",28,0,random(-1,1),2,random(-1,1))
		SKFP E 10 A_SpawnItem("HeadshotTarget10i",0,40,0)
		TNT1 A 0 A_JumpIfInventory("loadedOPG_Cryo",0,"CryoReload")
		TNT1 A 0 A_MonsterRefire(64,"See")
		Loop
	Reload:
		TNT1 A 0 A_ChangeFlag(NOPAIN,1)
		TNT1 A 0 A_TakeInventory("KnightAmmo",30)
		SKFP EE 3 A_SpawnItem("HeadshotTarget3i",0,40,0)
		TNT1 A 0 A_PlaySound("FNS_R2")
		SKFP E 2 A_SpawnItem("HeadshotTarget2i",0,40,0)
		SKFP EEEEEEEEEEEEEEEEEEEEEEE 4 A_SpawnItem("HeadshotTarget4i",0,40,0)
		TNT1 A 0 A_ChangeFlag(NOPAIN,0)
		Goto See
	CryoReload:
		TNT1 A 0 A_ChangeFlag(NOPAIN,1)
		TNT1 A 0 A_TakeInventory("loadedOPG_Cryo",60)
		SKFA EE 10 A_SpawnItem("HeadshotTarget10i",0,40,0)
		TNT1 A 0 A_PlaySoundEx("OPG/OPEN","SoundSlot5")
		SKFA E 10 A_SpawnItem("HeadshotTarget10i",0,40,0)
		SKFA EE 3 A_SpawnItem("HeadshotTarget3i",0,40,0)
		TNT1 A 0 A_PlaySoundEx("OPG/CLIPOUT","SoundSlot5")
		SKFA EEE 10 A_SpawnItem("HeadshotTarget10i",0,40,0)
		SKFA E 3 A_SpawnItem("HeadshotTarget3i",0,40,0)
		SKFA E 2 A_SpawnItem("HeadshotTarget2i",0,40,0)
		TNT1 A 0 A_PlaySoundEx("OPG/CLIPIN","SoundSlot5")
		SKFA EE 10 A_SpawnItem("HeadshotTarget10i",0,40,0)
		SKFA EE 3 A_SpawnItem("HeadshotTarget3i",0,40,0)
		TNT1 A 0 A_PlaySoundEx("OPG/CLOSE","SoundSlot5")
		SKFA E 10 A_SpawnItem("HeadshotTarget10i",0,40,0)
		SKFA E 2 A_SpawnItem("HeadshotTarget2i",0,40,0)
		TNT1 A 0 A_PlaySoundEx("OPG/BOLT","SoundSlot5")
		SKFA EEE 10 A_SpawnItem("HeadshotTarget4i",0,40,0)
		SKFA E 2 A_SpawnItem("HeadshotTarget4i",0,40,0)
		TNT1 A 0 A_ChangeFlag(NOPAIN,0)
		Goto See
	DistanceCheck:
		TNT1 A 0 A_JumpIfCloser(1768,"CloseRangeSpecial")
		TNT1 A 0 A_TakeInventory("DealWithIt",1)
		Goto BulletAttack
	CloseRangeSpecial:
		TNT1 A 0 A_SpawnItem("HeadshotTarget4i",0,40,0)
		SKFP E 4 A_FaceTarget
		TNT1 A 0 A_JumpIfCloser(192,"KnifeLunge")
		Goto LaunchIceGrenade
	LaunchIceGrenade:
		SKFP F 2 BRIGHT A_CustomMissile("KnightIceGrenadeShell",28,5,random(-15,15),2,random(0,10))
		Goto LaunchGrenadeFinish
	LaunchGrenadeFinish:
		SKFP EE 3 A_SpawnItem("HeadshotTarget3i",0,40,0)
		Goto GrenadeReload
	GrenadeReload:
		TNT1 A 0 A_ChangeFlag(NOPAIN,1)
		TNT1 A 0 A_PlaySoundEx("M4_CLIPO","SoundSlot5")
		SKFP E 2 A_SpawnItem("HeadshotTarget2i",0,40,0)
		SKFP E 10 A_SpawnItem("HeadshotTarget10i",0,40,0)
		TNT1 A 0 A_PlaySoundEx("M4_CLIPI","SoundSlot5")
		SKFP E 2 A_SpawnItem("HeadshotTarget2i",0,40,0)
		SKFP EEEEEE 10 A_SpawnItem("HeadshotTarget10i",0,40,0)
		TNT1 A 0 A_ChangeFlag(NOPAIN,0)
		Goto See
	KnifeLunge:
		TNT1 A 0 ThrustThing(angle*256/360,25,0,0)
		TNT1 A 0 A_SpawnItem("HeadshotTarget4i",0,40,0)
		SKFP E 4 A_FaceTarget
		TNT1 A 0 A_PlaySound("knife/swing")
		TNT1 A 0 A_SpawnItem("HeadshotTarget2i",0,40,0)
		SKFP E 2 A_CustomMeleeAttack(34,"knife/swing","knife/swing","Knife",1)
		Goto CloseRangeFinish
	CloseRangeFinish:
		SKFP EE 3 A_SpawnItem("HeadshotTarget3i",0,40,0)
		Goto See

/******************************************************************************\
==================================PAIN STATES===================================
\******************************************************************************/

	Pain:
		SKFP G 3 A_SpawnItem("HeadshotTarget4i",0,40,0)
		TNT1 A 0 A_SpawnItem("HeadshotTarget4i",0,40,0)
		SKFP G 3 A_Pain
		Goto See
	Pain.Shock:
		TNT1 A 0 A_ChangeFlag(NOPAIN,1)
		TNT1 A 0 A_Pain
		SKFP GGGGGGGGGGGGGGGGGGGGGGGGGGGGGG 10 A_SpawnItem("HeadshotTarget10i",0,40,0)
		TNT1 A 0 A_ChangeFlag(NOPAIN,0)
		Goto See

/******************************************************************************\
==================================DEATH STATES==================================
\******************************************************************************/

	Death:
		TNT1 A 0 A_JumpIfHealthLower(-60,"Splode")
	Death.KnifeSlash:
	Death.KnifeStab:
	Death.KnifeSlashPower:
	Death.KnifeStabPower:
	Death.WakizashiSlash:
	Death.WakizashiStab:
	Death.WakizashiSlashPower:
	Death.WakizashiStabPower:
		SKFP H 5 A_AlertMonsters
		SKFP I 5 A_Scream
		SKFP J 5 A_Fall
		TNT1 A 0 A_Playsound("items/sbomb")
		TNT1 A 0 A_SpawnItem("IceExplosion")
		TNT1 A 0 A_SpawnItemEx("IceSmokeSpawner2",0,0,1,0,0,0,0,0,0)
		TNT1 A 0 Radius_Quake(3,15,0,16,0)
		SKFP KLM 5
		TNT1 A 0 A_Jump(5,"Regenerating")
		TNT1 A 0 A_GiveInventory("isDead",1)
		SKFP N -1
		Stop
	Regenerating:
		SKFP NNNNNNNNNNNNNNN 70
		SKFP N -1 Thing_Raise(0)
		Wait
	Splode:
		SKFP I 1 A_SpawnDebris("NashGore_Gib9",0)
		TNT1 A 0 A_SpawnDebris("NashGore_Gib10",0)
		TNT1 A 0 A_SpawnDebris("NashGore_Gib11",0)
		TNT1 A 0 A_SpawnDebris("NashGore_Gib11",0)
		TNT1 A 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,0)
		TNT1 A 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,0)
		TNT1 A 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,0)
		TNT1 A 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,0)
		TNT1 A 0 A_Playsound("items/sbomb")
		TNT1 A 0 A_SpawnItem("IceExplosion")
		TNT1 A 0 A_SpawnItemEx("IceSmokeSpawner2",0,0,1,0,0,0,0,0,0)
		TNT1 A 0 Radius_Quake(3,15,0,16,0)
		TNT1 A 0 A_XScream
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_GiveInventory("isDead",1)
		TNT1 A 0 A_SpawnItemEx("DEADFStrikeKnightHead",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
		Stop
	Death.Fire2:
		TNT1 A 0 A_Playsound("items/sbomb")
		TNT1 A 0 A_SpawnItem("IceExplosion")
		TNT1 A 0 A_SpawnItemEx("IceSmokeSpawner2",0,0,1,0,0,0,0,0,0)
		TNT1 A 0 Radius_Quake(3,15,0,16,0)
		TNT1 A 0 A_GiveInventory("isDead",1)
		TNT1 A 1 A_SpawnItem("BurnDeath1", 2, 1)
		TNT1 A 0 A_AlertMonsters
		Stop
	Death.head:
		SKFP O 9 A_PlaySound("headshot/splat")
		TNT1 A 0 A_SpawnItem("AeniBlood",0,40,0)
		TNT1 A 0 A_SpawnItem("AeniBlood",0,40,0)
		SKFP P 9 A_XScream
		SKFP Q 9 A_Fall
		SKFP R 5 A_GiveInventory("isDead",1)
		SKFP S 5 A_SpawnItemEx("DEADFStrikeKnight",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
		Stop
	Raise:
		SKFP KJI 5
		TNT1 A 0 A_ChangeFlag("FRIENDLY",1)
		TNT1 A 0 A_TakeInventory("isDead",1)
		/*TNT1 A 0 A_CustomMissile("OrbitFriendly",44,0,0,2,0)
		TNT1 A 0 A_CustomMissile("OrbitFriendly2",44,0,180,2,0)*/
		SKFP H 5 Thing_SetSpecial(0,226,613,0,0)
		Goto See
	}
}

actor DPP_Cryo : FastProjectile {
	Radius 2
	Height 4
	Speed 80
	Damage 10
	Projectile
	//+HEXENBOUNCE
	+RANDOMIZE
	-BLOODSPLATTER
	RenderStyle Add
	Alpha 0.75
	scale 0.4
	SeeSound "weapons/iceshot"
	DeathSound "weapons/icehit"
	DamageType Ice
	Decal FreezeBlastMelt
	States {
		Spawn:
			CRYP ABCDEFGHIJKLMNOP 1 bright
			loop
		Death:
			ICPR DEFGH 4 bright
			//TNT1 A 0 A_PlaySound("misc/freeze",0)
			TNT1 A 0 BRIGHT A_SpawnItemEx("IceCrystal",0,0,0,random(-16,16))
			stop
	}
}