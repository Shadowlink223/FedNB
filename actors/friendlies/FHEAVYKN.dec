ACTOR FHeavyKnight : HeavyKnight 11204 {
	//$Category Federation
	
	Health 700
	DamageFactor "KillMe", 0.0
	DamageFactor "FriendBullet", 0.5
	+NOBLOCKMONST
	+FRIENDLY
	
	States {

/******************************************************************************\
==================================MAIN STATES===================================
\******************************************************************************/

	Spawn:
		HEVY A 0
		/*TNT1 A 0 A_CustomMissile("OrbitFriendly",44,0,0,2,0)
		TNT1 A 0 A_CustomMissile("OrbitFriendly2",44,0,180,2,0)*/
		TNT1 A 0 A_ChangeFlag("FRIENDLY",1)
		TNT1 A 0 ACS_ExecuteAlways(556,0)
		TNT1 A 0 Thing_SetSpecial(0,226,613,0,0)
		TNT1 A 0 A_SpawnItem ("KillMe")
	SpawnLoop:
		TNT1 A 0 A_SpawnItem("HeadshotTarget10",15,40,0)
		G99P A 10 A_LookEx(0,0,random(512,2048),random(1024,4096),random(90,180),"See")
		Loop
	See:
		G99P A 1 // Infinite loop crash prevention?
		Goto IfLowHealth
	IfLowHealth:
		TNT1 A 0 A_JumpIfHealthLower(50,"LowHealth")
		TNT1 A 0 A_TakeInventory("DealWithIt",1)
		Goto HealthHigh
	HealthHigh:
		TNT1 A 0 A_ChangeFlag("Frightened",0)
		Goto Chase
	LowHealth:
		TNT1 A 0 A_Jump(32,"HealUp")
		TNT1 A 0 A_TakeInventory("DealWithIt",1)
		Goto Retreat
	Retreat:
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_ChangeFlag("Frightened",1)
		Goto HealUp
	HealUp:
		TNT1 A 0 HealThing(1)
		Goto Chase
	Chase:
		TNT1 A 0 A_SpawnItem("HeadshotTarget3",15,40,0)
		G99P A 3 A_Chase
		TNT1 A 0 A_SpawnItem("HeadshotTarget3",15,40,0)
		G99P B 3 A_Chase
		TNT1 A 0 A_SpawnItem("HeadshotTarget3",15,40,0)
		G99P C 3 A_Chase
		TNT1 A 0 A_SpawnItem("HeadshotTarget3",15,40,0)
		G99P D 3 A_Chase
		Goto See

/******************************************************************************\
=================================ATTACK STATES==================================
\******************************************************************************/

	Missile:
		TNT1 A 0 A_SpawnItem("HeadshotTarget10",0,40,0)
		G99P E 10 A_FaceTarget
		TNT1 A 0 A_Jump(64,"DistanceCheck")
		Goto BulletAttack
	BulletAttack:
		TNT1 A 0 A_JumpIfInventory("KnightAmmo",100,"Reload")
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 Radius_Quake(1,8,0,1,0)
		TNT1 A 0 A_PlaySound("M60/Fire",6)
		TNT1 A 0 A_GiveInventory("KnightAmmo",1)
		TNT1 A 0 A_CustomBulletAttack(11.25,11.25,1,random(1,5)*3,"FBulletPuff")
		//TNT1 A 0 A_SpawnItem("HeadshotTarget1",0,40,0)
		G99P F 1 A_CustomMissile("FHeavyTracer",38,0,random(-11.25,11.25),0,random(-11.25,11.25))
		TNT1 A 0 A_SpawnItem("HeadshotTarget1",0,40,0)
		G99P F 1 BRIGHT A_SpawnItem("HeadshotTarget1", 0, 40,0)
		G99P F 2 A_SpawnItem("HeadshotTarget2",0,40,0)
		TNT1 A 0 A_JumpIfInventory("KnightAmmo",100,"Reload")
		TNT1 A 0 A_MonsterRefire(246,"See")
		Loop
	Reload:
		TNT1 A 0 A_ChangeFlag(NOPAIN,1)
		TNT1 A 0 A_TakeInventory("KnightAmmo",100)
		TNT1 A 0 A_PlaySoundEx("M60/BeltOut","SoundSlot5")
		G99P EE 3 A_SpawnItem("HeadshotTarget3",0,40,0)
		G99P EEEEEEE 10 A_SpawnItem("HeadshotTarget10",0,40,0)
		TNT1 A 0 A_PlaySoundEx("M60/BoxOut","SoundSlot5")
		G99P EE 3 A_SpawnItem("HeadshotTarget3",0,40,0)
		G99P EE 10 A_SpawnItem("HeadshotTarget10",0,40,0)
		TNT1 A 0 A_PlaySoundEx("M60/BoxIn","SoundSlot5")
		G99P EEE 10 A_SpawnItem("HeadshotTarget10",0,40,0)
		TNT1 A 0 A_PlaySoundEx("M60/Open","SoundSlot5")
		G99P E 4 A_SpawnItem("HeadshotTarget4",0,40,0)
		G99P EE 10 A_SpawnItem("HeadshotTarget10",0,40,0)
		TNT1 A 0 A_PlaySoundEx("M60/BeltIn","SoundSlot5")
		G99P EEEEEE 10 A_SpawnItem("HeadshotTarget10",0,40,0)
		TNT1 A 0 A_PlaySoundEx("M60/Close","SoundSlot5")
		G99P E 2 A_SpawnItem("HeadshotTarget2",0,40,0)
		G99P E 10 A_SpawnItem("HeadshotTarget10",0,40,0)
		TNT1 A 0 A_PlaySoundEx("M60/Cock","SoundSlot5")
		G99P EE 3 A_SpawnItem("HeadshotTarget3",0,40,0)
		G99P E 10 A_SpawnItem("HeadshotTarget10",0,40,0)
		TNT1 A 0 A_ChangeFlag(NOPAIN,0)
		Goto See
	DistanceCheck:
		G99P E 0 A_JumpIfCloser(192,"KnifeLunge")
		TNT1 A 0 A_JumpIfCloser(1768,"TossGrenade")
		G99P E 0 A_Jump(128,"RocketAttack")
		TNT1 A 0 A_TakeInventory("DealWithIt",1)
		Goto BulletAttack
	KnifeLunge:
		TNT1 A 0 A_SpawnItem("HeadshotTarget4",0,40,0)
		TNT1 A 0 ThrustThing(angle*256/360,25,0,0)
		G99P E 4 A_FaceTarget
		TNT1 A 0 A_PlaySound("knife/swing")
		TNT1 A 0 A_SpawnItem("HeadshotTarget2",0,40,0)
		G99P E 2 A_CustomMeleeAttack(34,"knife/swing","knife/swing","Knife",1)
		Goto CloseRangeFinish
	CloseRangeFinish:
		G99P EE 3 A_SpawnItem("HeadshotTarget3",0,40,0)
		Goto See
	TossGrenade:
		TNT1 A 0 A_SpawnItem("HeadshotTarget4",0,40,0)
		G99P F 4 A_CustomMissile("HeavyGrenade",32,0,random(16,-16),0)
		G99P E 10 A_SpawnItem("HeadshotTarget10",0,40,0)
		Goto See
	RocketAttack:
		TNT1 A 0 A_TakeInventory("KnightSpecial",1000)
		Goto RocketAttackLoop
	RocketAttackLoop:
		TNT1 A 0 A_JumpIfInventory("KnightSpecial",6,"FireRocket")
		TNT1 A 0 A_SpawnItemEx("RLsmoke",-10,0,32,3,random(-1,1),random(-1,1),180,SXF_NOCHECKPOSITION )
		TNT1 A 0 A_SpawnItem("HeadshotTarget4",0,40,0)
		G99P E 4 A_FaceTarget
		TNT1 A 0 A_SpawnItem("HeadshotTarget4",0,40,0)
		G99P E 4 A_FaceTarget
		TNT1 A 0 A_GiveInventory("KnightSpecial",1)
		Loop
	FireRocket:
		TNT1 A 0 A_SpawnItem("HeadshotTarget10",0,40,0)
		G99P F 10 A_CustomMissile("HeavyRocket",24,0,random(16,-16),0)
		Goto See


/******************************************************************************\
==================================PAIN STATES===================================
\******************************************************************************/

	Pain:
		G99P G 3 A_SpawnItem("HeadshotTarget4",0,40,0)
		TNT1 A 0 A_SpawnItem("HeadshotTarget4",0,40,0)
		G99P G 3 A_Pain
		Goto Missile
	Pain.Shock:
		TNT1 A 0 A_ChangeFlag(NOPAIN,1)
		TNT1 A 0 A_Pain
		G99P GGGGGGGGGGGGGGGGGGGGGGGGGGGGGG 10 A_SpawnItem("HeadshotTarget10",0,40,0)
		TNT1 A 0 A_ChangeFlag(NOPAIN,0)
		Goto See

/******************************************************************************\
==================================DEATH STATES==================================
\******************************************************************************/

	Death: 
		TNT1 A 0 A_JumpIfHealthLower(-60,"Splode")
	Death.KnifeSlash:
	Death.KnifeStab:
	Death.KnifeSlashPower:
	Death.KnifeStabPower:
	Death.WakizashiSlash:
	Death.WakizashiStab:
	Death.WakizashiSlashPower:
	Death.WakizashiStabPower:
		G99P H 5
		G99P I 5 A_Scream
		G99P J 5 A_Fall
		G99P KLM 5
		TNT1 A 0 A_Jump(5,"Regenerating")
		TNT1 A 0 A_GiveInventory("isDead",1)
		G99P N -1
		Stop
	Regenerating:
		G99P NNNNNNNNNNNNNNN 70
		G99P N -1 Thing_Raise(0)
		Wait
	Splode:
		G99P I 1 A_SpawnDebris("NashGore_Gib9",0)
		TNT1 A 0 A_SpawnDebris("NashGore_Gib10",0)
		TNT1 A 0 A_SpawnDebris("NashGore_Gib11",0)
		TNT1 A 0 A_SpawnDebris("NashGore_Gib11",0)
		TNT1 A 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,0)
		TNT1 A 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,0)
		TNT1 A 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,0)
		TNT1 A 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,0)
		TNT1 A 0 A_XScream
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_GiveInventory("isDead",1)
		TNT1 A 0 A_SpawnItemEx("DEADFInitiateHead",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
		Stop
	Death.Fire2:
		TNT1 A 0 A_GiveInventory("isDead",1)
		TNT1 A 1 A_SpawnItem("BurnDeath1",2,1)
		TNT1 A 0 A_AlertMonsters
		Stop
	Death.head:
		G99P O 9 A_PlaySound("headshot/splat")
		TNT1 A 0 A_SpawnItem("AeniBlood",0,40,0)
		TNT1 A 0 A_SpawnItem("AeniBlood",0,40,0)
		G99P P 9 A_XScream
		G99P Q 9 A_Fall
		G99P R 5 A_GiveInventory("isDead",1)
		G99P S 5 A_SpawnItemEx("DEADFInitiate",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
		stop
	Raise: 
		G99P KJI 5
		TNT1 A 0 A_ChangeFlag("FRIENDLY",1)
		TNT1 A 0 A_TakeInventory("isDead",1)
		/*TNT1 A 0 A_CustomMissile("OrbitFriendly",44,0,0,2,0)
		TNT1 A 0 A_CustomMissile("OrbitFriendly2",44,0,180,2,0)*/
		G99P H 5 Thing_SetSpecial(0,226,613,0,0)
		Goto See
	}
}