Actor KnightGrenade {
  obituary "%o was fragged by a Knight's grenade."
  radius 6
  height 8
  speed 20
  damage (10)
  seesound "ZSec/bounce"
  PROJECTILE
  +DOOMBOUNCE
  -NOGRAVITY
  +GRENADETRAIL
  +DONTFALL
  bouncefactor 0.5
  bouncecount 10
  states
  {
  Spawn:
	GRND AAA 35 bright
  Death:
	MISL C 0 bright A_PlaySound("weapons/rocklx")
	MISL C 0 bright A_ChangeFlag(NoGravity, 1)
	TNT1 A 0 A_SpawnItemEx("DustCloudCluster",0,0,0,0,0,0,0,0,0)
	MISL C 5 bright A_Explode(30,128,1)
	MISL D 4 bright
	MISL E 3 bright
	stop
  }
}

Actor KnightSmokeGrenade {
  obituary "%o was fragged by a Knight's grenade."
  radius 6
  height 8
  speed 20
  damage (10)
  seesound "ZSec/bounce"
  PROJECTILE
  +DOOMBOUNCE
  -NOGRAVITY
  +GRENADETRAIL
  +DONTFALL
  bouncefactor 0.5
  bouncecount 10
  states
  {
  Spawn:
	GRND AAA 35 bright
  Death:
	MISL C 0 bright A_PlaySound("weapons/rocklx")
	MISL C 0 bright A_ChangeFlag(NoGravity, 1)
	MISL C 5 bright A_CustomMissile("smokebomb")
	MISL D 4 bright
	MISL E 3 bright
	stop
  }
}

// RGA3 -- Voltlock Grenade Shell Decorate

Actor KnightGrenadeShell {
	radius 5
	height 5
	speed 60
	damage (10)
	
	SeeSound "RPG/MissileLaunch"
	deathsound "ZSec/bounce"
	
	PROJECTILE
	-NOGRAVITY
	+RANDOMIZE
	+NOEXTREMEDEATH
	+FORCERADIUSDMG
	+CANBOUNCEWATER
	-NOTELEPORT
	+BLOODSPLATTER
	
	gravity 0.3
	bouncefactor 0.5
	
	DamageType "GrenadeImpact"
	
	obituary "%o was punched by a Knight's grenade."
	
	states {
		Spawn:
			GRND A 0
			GRND AAAA 1 A_SpawnItemEx("MiniBombTrail", 0, 0, 0, 0, 0, 0, 0, 128, 0)
			GRND A 0 A_GiveInventory("KnightGrenadeArmed",1)
			Loop
		XDeath:
			TNT1 A 0 //A_GiveToTarget("HitIndicator",1)
		Death:
			TNT1 A 0 A_JumpIfInventory("KnightGrenadeArmed",1,"DeathArmed")
			TNT1 A 0 A_Explode(30,16,0)
			TNT1 A 20 A_SpawnItemEx("KnightGrenadeShellUnarmedDrop",0,0,0,-2,0,2,random(-8,8),0,0)
			stop
		DeathArmed:
			TNT1 A 0 A_AlertMonsters
			TNT1 A 0 A_SpawnItemEx("KnightGrenadeShellExplosion",-1,0,0,0,0,0,0,0,0)
			TNT1 A 0 A_SpawnItemEx("KnightGrenadeShock",-1,0,0,0,0,0,0,0,0)
			TNT1 A 0 A_SpawnItemEx("DustCloudCluster",0,0,0,0,0,0,0,0,0)
			TNT1 A 0 A_PlaySoundEx("Artillery/KABOOM","SoundSlot5")
			TNT1 A 0 A_PlaySoundEx("farexp","SoundSlot6")
			TNT1 A 15 Radius_Quake(9,15,0,8,0)
			TNT1 A 15 Radius_Quake(5,15,0,16,0)
			TNT1 A 15 Radius_Quake(1,15,0,125,0)
			stop
	}
}

actor KnightGrenadeShellExplosion {
	Speed 1
	Damage 0
	
	RenderStyle None
	
	PROJECTILE
	-RANDOMIZE
	+NOCLIP
	
	Scale 0.7
	
	obituary "%o was fragged by a Knight's grenade."
	
	DamageType "Explosion"
	
	states {
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_Explode(15,152,0)
			TNT1 A 5 A_Explode(5,72)
			stop
	}
}

actor KnightGrenadeShock {
	Scale 0.7
	
	RenderStyle None
	
	PROJECTILE
	-RANDOMIZE
	+NOCLIP
	
	Speed 1
	Damage 0
	
	obituary "%o was fataly shellshocked by a Knight's grenade."
	
	DamageType "Shock"
	
	states {
		Spawn:
			TNT1 A 1
			TNT1 A 5 A_Explode(5,80)
			stop
	}  
}

ACTOR KnightGrenadeShellUnarmedDrop {
	Speed 1
	Height 1
	Radius 0
	
	gravity 0.5
	bouncefactor 0.5
	bouncecount 3
	
	+DOOMBOUNCE
	+MISSILE
	
	SeeSound "ZSec/bounce"
	DeathSound "ZSec/bounce"
	
	States {
		Spawn:
			GRND A 0 
			GRND AAAAA 5 A_SpawnItemEx("MiniBombTrail", 0, 0, 0, 0, 0, 0, 0, 128, 0)
			GRND A -1
			Loop
		Death:
			GRND A 1050
			Stop
	}
}

Actor KnightGrenadeArmed : Inventory {
    inventory.amount 0
    inventory.maxamount 1
}

Actor KnightBioGrenadeShell : KnightGrenadeShell {
	States {
		DeathArmed:
			TNT1 A 0 A_AlertMonsters
			TNT1 A 0 A_SpawnItem("BioSludgeExplosion")
			TNT1 A 0 A_SpawnItemEx("PoisonSmokeSpawner2",0,0,1,0,0,0,0,0,0)
			TNT1 A 0 A_Playsound("items/sbomb")
			TNT1 A 15 Radius_Quake(3,15,0,16,0)
			stop
	}
}

Actor KnightIceGrenadeShell : KnightGrenadeShell {
	States {
		DeathArmed:
			TNT1 A 0 A_AlertMonsters
			TNT1 A 0 A_SpawnItem("IceExplosion")
			TNT1 A 0 A_SpawnItemEx("IceSmokeSpawner2",0,0,1,0,0,0,0,0,0)
			TNT1 A 0 A_Playsound("items/sbomb")
			TNT1 A 15 Radius_Quake(3,15,0,16,0)
			stop
	}
}

Actor KnightFireGrenadeShell : KnightGrenadeShell {
	States {
		DeathArmed:
			TNT1 A 0 A_AlertMonsters
			TNT1 A 0 A_CustomMissile("FKFireDie",5,0,90)
			TNT1 A 0 A_CustomMissile("FKFireDie",5,0,45)
			TNT1 A 0 A_CustomMissile("FKFireDie",5,0,-90)
			TNT1 A 0 A_CustomMissile("FKFireDie",5,0,-45)
			TNT1 A 0 A_CustomMissile("FKFireDie",5,0,180)
			TNT1 A 0 A_CustomMissile("FKFireDie",5,0,135)
			TNT1 A 0 A_CustomMissile("FKFireDie",5,0,0)
			TNT1 A 0 A_CustomMissile("FKFireDie",5,0,-135)
			TNT1 A 0 A_SpawnItemEx("DustCloudCluster_NoDirt",0,0,0,0,0,0,0,0,0)
			TNT1 A 15 Radius_Quake(3,15,0,16,0)
			stop
	}
}

Actor KnightShockGrenadeShell : KnightGrenadeShell {
	States {
		DeathArmed:
			TNT1 A 0 A_AlertMonsters
			TNT1 A 0 A_PlaySound("darkinquisitor/empdie")
			TNT1 A 2 A_SpawnItemEx("TeslaShotAltExplode1",0,0,0,0,0,0,32,0)
			TNT1 AAAAAAAAAA 2 A_SpawnItemEx("TeslaShotAltExplode1",random(-128,128),random(-128,128),random(-128,128),0,0,0,32,0)
			stop
	}
}