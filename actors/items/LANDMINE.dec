ACTOR LandMine 21400 {
	Radius 2
	Height 2
	Health 1
	Speed 0
	+SHOOTABLE
	+SOLID
	+CANPASS
	+QUICKTORETALIATE
	//+TOUCHY
	+LOOKALLAROUND
	-NOGRAVITY
	+NOBLOOD
	+DONTGIB
	+NOICEDEATH
	HitObituary "%o didn't watch their step."
	Obituary "%o didn't watch their step."
	States {
		Spawn:
			FMNE A 1 A_Look
			Loop
		See:
			FMNE A 1 A_Chase("Melee","")
			TNT1 A 0 A_ClearTarget
			Goto Spawn
		Melee:
			TNT1 A 0 A_Die
			Goto Death
		Death:
			FMNE A 12
			TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("MineSmoke",0,0,4,Random(-2,2),Random(-2,2),Random(2,10),0,SXF_NOCHECKPOSITION,SXF_CLIENTSIDE)
			TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("MineSmokeSlow",0,0,4,Random(-3,3),Random(-3,3),Random(2,4),0,SXF_NOCHECKPOSITION,SXF_CLIENTSIDE)
			TNT1 A 0 A_PlaySound("Mine/LandMineExplosion",CHAN_AUTO)
			TNT1 A 0 A_PlaySound("Mine/LandMineExplosionDistant",CHAN_AUTO,0,ATTN_NONE)
			TNT1 A 0 A_Explode(64,256,1,1)
			Stop
	}
}

ACTOR FLandMine : LandMine 21401 { +FRIENDLY }

ACTOR MineSmoke {
	Radius 1
	Height 2
	Speed 1
	Damage 0
	PROJECTILE
	+CLIENTSIDEONLY
	+RIPPER
	+BLOODLESSIMPACT
	+PAINLESS
	Alpha 0.5
	Decal Scorch
	States {
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_Jump(256,"1","2","3","4")
			Stop
		1:
			TSM1 ABCDEFGHI 3 A_FadeOut(0.05)
			Stop
		2:
			TSM2 ABCDEFGHI 3 A_FadeOut(0.05)
			Stop
		3:
			TSM3 ABCDEFGHI 3 A_FadeOut(0.05)
			Stop
		4:
			TSM4 ABCDEFGHI 3 A_FadeOut(0.05)
			Stop
		Death:
			TNT1 A 0
			Stop
	}
}

ACTOR MineSmokeSlow : MineSmoke {
	States {
		1:
			TSM1 ABCDEFGHI 5 A_FadeOut(0.05)
			Stop
		2:
			TSM2 ABCDEFGHI 5 A_FadeOut(0.05)
			Stop
		3:
			TSM3 ABCDEFGHI 5 A_FadeOut(0.05)
			Stop
		4:
			TSM4 ABCDEFGHI 5 A_FadeOut(0.05)
			Stop
	}
}